== ユーティリティ

`starling.utils` パッケージにはいくつか便利なクラスや命令が存在します。見逃さないようにしましょう。

=== Colors

従来の Flash も Starling も、色の情報は16進数の値で表されます。

//原文：Here are a few examples:
//翻訳なしで良さげ

[source, as3]
----
// format:         0xRRGGBB
var red:Number   = 0xff0000;
var green:Number = 0x00ff00; // もしくは 0xff00
var blue:Number  = 0x0000ff; // もしくは 0xff
var white:Number = 0xffffff;
var black:Number = 0x000000; // もしくは 0
----

Color クラスはいくつかの色味に名前をつけて保持しています。
また、その色の各チャンネル（RGB成分）には簡単にアクセスする事ができます。

[source, as3]
----
var purple:uint = Color.PURPLE; // <1>
var lime:uint   = Color.LIME;
var yellow:uint = Color.YELLOW;

var color:uint = Color.rgb(64, 128, 192); // <2>

var red:int   = Color.getRed(color);   // ->  64 <3>
var green:int = Color.getGreen(color); // -> 128
var blue:int  = Color.getBlue(color);  // -> 192
----
<1> いくつかの色はすでに定義済みです。
<2> 他の色はこのメソッドで作成する事ができます。RGB 値を引数としていください。(範囲は0〜255。)
<3> それぞれのチャンネルの値を整数値として得ることも可能です。

=== Angles

Starling 角度を'ラジアン'の単位で統一して扱います。（Flash では'度'と'ラジアン'の単位が場所によって混在して使われています。）
'度'と'ラジアン'を変換するには下記のようなコードで行えます。

[source, as3]
----
var degrees:Number = rad2deg(Math.PI); // -> 180
var radians:Number = deg2rad(180);     // -> PI
----

=== StringUtil

_.NetやC#_ と同じように、文字列の装飾に `format` メソッドが利用可能です。

[source, as3]
----
StringUtil.format("{0} plus {1} equals {2}", 4, 3, "seven");
  // -> "4 plus 3 equals seven"
----

このクラスには、文字列の最初と最後に存在する空白文字をトリミングするメソッドも存在します。
これはユーザが入力した文字列を扱う際によく必要となる処理です。

[source, as3]
----
StringUtil.trim("  hello world\n"); // -> "hello world"
----

=== SystemUtil

アプリまたはゲームが実行されている環境の情報を得ることは役に立ちます。
_SystemUtil_ クラスはそのためのメソッドやプロパティをいくつか持っています。

[source, as3]
----
SystemUtil.isAIR; // AIR で動いているか、Flash で動いているか。
SystemUtil.isDesktop; // デスクトップで動いているか、モバイルで動いているか。
SystemUtil.isApplicationActive; // 最小化されていないか。
SystemUtil.platform; // 動作プラットフォームの種類。WIN(Windows)、MAC(Mac)、LNX(Linux)、IOS(iOS)、AND(Android)。
----

=== MathUtil

//原文：While that class is mainly designed to help with some geometric problems,
このクラスは主に幾何学的な問題を扱う際に役に立つようデザインされていますが、
下記のように便利なヘルパーメソッドも持っています。

[source, as3]
----
var min:Number = MathUtil.min(1, 10); // <1>
var max:Number = MathUtil.max(1, 10); // <2>
var inside:Number = MathUtil.clamp(-5, 1, 10); // <3>
----
<1> ２つの数字のうち小さいものを返します。ここでの結果は、`1`
<2> ２つの数字のうち大きいものを返します。ここでの結果は、`10`
<3> 一つ目の引数の数値を、残りの引数で指定された範囲に収めます。ここでの結果は、`1`

もしあなたが  _AS3_ でプログラムを組んだ事があるならば、
なぜすでに `Math` クラスで提供されているのと同じような機能をわざわざ新しく作ったのかと疑問に思うでしょう。

残念なことに、`Math` クラスのそれらには好ましくない副作用があるのです。
//原文：each time you call e.g. `Math.min`, it creates a temporary object (at least when you compile your app for _iOS_, that is).
`Math.min` メソッドを実行するたびに、一時的なオブジェクトが（処理の裏で）作られてしまうのです。
（少なくとも _iOS_ 向きにアプリケーションをコンパイルした際は。）
//原文；Those alternatives do not have this side effect, so you should always prefer them.
MathUtil のそれはそのような副作用がありません。よって、どんなときも MathUtil のメソッドを使うのが良いでしょう。

=== オブジェクトプーリング

さて、上記で一時的なオブジェクトについて触れたこともあり
今が _Pool_ オブジェクトについて紹介するのにちょうどいいタイミングだと思われます。

経験ある AS3 開発者なら知っている事ですが、どんなオブジェクトの生成にもコストが発生します。
また、オブジェクトが使ったメモリ空間は利用後に回収される必要があります。（いわゆるガベージコレクション）
この処理は自動的に行われ、たいていの場合は、いつ行われたのか気づくこともできません。

しかし、このメモリ回収処理に時間がかかりすぎると、アプリケーションは一瞬フリーズをしてしまいます。
//原文：If that happens often, it quickly becomes a nuisance to your users.
このフリーズが頻繁に起こると、ユーザー体験はすぐさま悪くなってしまいます。

この問題を避ける一つの方法は、作成したオブジェクトをリサイクルして何度も使い回す事です。
例えば、_Point_ クラスや、_Rectangle_ クラスはたいてい短期間しか必要とされず、
生成・利用後、すぐに廃棄されてしまいます。

今日からは、Starling の _Pool_ クラスにこれらのオブジェクトを管理させましょう。

[source, as3]
----
var point:Point = Pool.getPoint(); // <1>
doSomethingWithPoint(point);
Pool.putPoint(point); // <2>

var rect:Rectangle = Pool.getRectangle(); // <1>
doSomethingWithRectangle(rect);
Pool.putRectangle(rect); // <2>
----
<1> オブジェクトを pool から受け取ります。インスタンスの生成を置き換える処理です。
<2> オブジェクトの利用後、pool にオブジェクトを預けます。

このクラスは、_Vector3D_、_Matrix _および、_Matrix3D_ を同じようにサポートします。

IMPORTANT: 受け取りと預ける回数はそれぞれでバランスが取れているようにして下さい。
//原文：If you put too many objects into the pool and never retrieve them, it will fill up over time, using more and more memory.
もしもあまりにもたくさんのオブジェクトを pool に預けて、そのまま取り出さない場合、
どんどんとそこに溜まっていき、メモリを浪費してしまいます。

=== その他

`starling.utilities` パッケージには、ここで紹介されても良いぐらいのヘルパークラスがまだまだあります。
完全なメソッドとプロパティの一覧を得るには、 http://doc.starling-framework.org/core/starling/utils/package-detail.html[API Reference] を確認してください。一見の価値あり、です！
