== アセット管理

どのアプリケーションでもテクスチャがリソースの大きな範囲をしめる事はここまでで明らかとなりました。
//原文：One thing should be clear by now: textures make up a big part of every application's resources.
特にゲームではたくさんのギラフィックを必要とします。具体的には、ユーザインタフェース、キャラクター、アイテム、背景画像などです。
しかし、それで全てではありません。サウンドファイルや設定ファイルもおそらく必要となるでしょう。

これらのアセットにアクセスするには、いくつかの方法があります。

* アプリケーションに `[Embed]` メタデータ指定で埋め込んでしまう。
* ストレージからそれらを読み込む。 (AIR アプリケーションにでのみ可能です。)
* 例えば Webサーバなどの URL からロードする。

それぞれのやり方で異なったコーディングが必要なため（アセットのタイプとロードの仕組みによります。）、統一した方法でアセットにアクセスするのは難しいのです。
幸運にも、Starling にはそれをうまく扱ってくれる _AssetManager_ というクラスを含まれています。

_AssetManager_ は以下のタイプのアセット読み込みをサポートします。

* テクスチャ (ビットマップでも ATF 形式でも)
* テクスチャアトラス
* ビットマップフォント
* サウンド
* XML データ
* JSON データ
* その他バイナリファイル

アセットの読み込みを完了するまで、 AssetManager は３ステップのアプローチを取ります。

1. 読み込みたいアセットの "ポインタ" をキューに積みます。"ポインタ" とは、`File` オブジェクトや URL などです。
2. キューの処理実行を AssetManager に伝えます。
3. キューの読み込みが完了次第、いくつかの `get` で始まるメソッドでアセットにアクセスが可能になります。

[TIP]
====
AssetManager には `verbose` プロパティが存在します。
これが 有効（true）に設定されると、キューイングとローディングの全てのステップがコンソールにログ出力されるようになります。
あるアセットがロードされない理由がわからない場合など、デバッグ作業にとても有効です。
この理由から、最新の Starling ではこのプロパティが有効とされています。
====

=== アセット読み込みのキュー管理

最初のステップは扱いたいファイルを全てキューに入れる事です。
//原文：The first step is to enqueue all the assets you want to use.
どのように処理されるかは、アセットのタイプとそれぞれの出どころによります。

==== ディスクまたはネットワークからのアセット読み込み

ファイルをキューに入れる祭、読み込み元がディスクもしくはリモートのサーバーである場合はわかりやすいです。

[source, as3]
----
// リモートURLからキューに積む
assets.enqueue("http://gamua.com/img/starling.jpg");

// ディスクからキューに積む (AIR のみ)
var appDir:File = File.applicationDirectory;
assets.enqueue(appDir.resolvePath("sounds/music.mp3"));

// 全てのファイルを再帰的にディレクトリから読み込む (AIR のみ).
assets.enqueue(appDir.resolvePath("textures"));
----

テクスチャアトラスを読み込む場合、XMLと対応したテクスチャをそのままキューに積めば良いです。
ただし、XML の `imagePath` 属性値に正しいファイル名が設定されている状態として下さい。
AssetManager が実際にアトラスを実体化する際にその属性値を参照するからです。

[source, as3]
----
assets.enqueue(appDir.resolvePath("textures/atlas.xml"));
assets.enqueue(appDir.resolvePath("textures/atlas.png"));
----

**ビットマップフォント** も同じように動きます。
その場合、XML の `file` 属性値がらdしい設定であるようにして下さい。

[source, as3]
----
assets.enqueue(appDir.resolvePath("fonts/desyrel.fnt"));
assets.enqueue(appDir.resolvePath("fonts/desyrel.png"));
----

==== 埋め込みアセットの読み込み

埋め込みアセットに対しては、全ての埋め込み処理を１つのクラスにまとめる事をおすすめします。
`public static const` としてそれらを宣言して、下記の命名規則に従って下さい。

* 埋め込まれた画像ファイルは、そのファイル名と完全に同じ名前を持つようにして下さい。その際、拡張子は含まなくて良いです。
これは、XML(アトラスまたはフォント設定)からのファイル参照が壊れないために必要です。

* アトラスとビットマップフォントの XML ファイルは任意の名前を取ることができます。何故ならばファイルネームで参照されることはないからです。

サンプルのクラスのコードは下記のようになります。

[source, as3]
----
public class EmbeddedAssets
{
    /* PNG texture */
    [Embed(source = "/textures/bird.png")]
    public static const bird:Class;

    /* ATF texture */
    [Embed(source   = "textures/1x/atlas.atf",
           mimeType = "application/octet-stream")]
    public static const atlas:Class;

    /* XML file */
    [Embed(source   = "textures/1x/atlas.xml",
           mimeType = "application/octet-stream")]
    public static const atlas_xml:Class;

    /* MP3 sound */
    [Embed(source = "/audio/explosion.mp3")]
    public static const explosion:Class;
}
----

このクラスをキューに積んだ場合、AssetManager は全ての埋め込まれたアセットをインスタンス化します。

[source, as3]
----
var assets:AssetManager = new AssetManager();
assets.enqueue(EmbeddedAssets); // <1>
----
<1> `bird` テクスチャーと、 `explosion` サウンドと、テクスチャアトラスがキューに積まれている。

==== アセットごとの設定

テクスチャを（`Texture.from...()` メソッドを通して）手作業で作る場合、細かい設定を行うチャンスです。
例えば、テクスチャのフォーマットとスケールファクター値をどのようにするか決めることができます。

それらの設定の問題は、一度テクスチャーが作られると、設定が変更できないという事です。
よってテクスチャーを作成する際は、確実に正しい設定がなされるようにしなくてはいけません。
AssetManager はこのような設定を行う事をサポートします。

[source, as3]
----
var assets:AssetManager = new AssetManager();
assets.textureFormat = Context3DTextureFormat.BGRA_PACKED;
assets.scaleFactor = 2;
assets.enqueue(EmbeddedAssets);
----

AssetManager はこれらの設定を内部で作成する全てのテクスチャに適用します。
しかし、このやり方では読み込んだ全てのテクスチャーに同じ設定をする事しか認めないように見えます。
しかし、実際にはそんな事はありません。いくつかの手続きを踏んで読み込みファイルをロードのキューに積む事で実現できます。
正しい設定をそれぞれの `enqueue` 実行前に行ってやればいいのです。

[source, as3]
----
assets.scaleFactor = 1;
assets.enqueue(appDir.resolvePath("textures/1x"));

assets.scaleFactor = 2;
assets.enqueue(appDir.resolvePath("textures/2x"));
----

上記のやり方で、`1x` と `2x` それぞれのフォルダから読み込んだテクスチャーに、正しいスケールファクター値である１と２を設定する事ができます。

=== アセットのロード

アセットがキューに積まれたので、まとめていっぺんにロードをする事ができます。
アセットの数とサイズによってはしばらく時間がかかります。
そのため、プログレスバーかローディングインジケーターを表示してユーザーに見えるのが良いでしょう。

[source, as3]
----
assets.loadQueue(function(ratio:Number):void
{
    trace("Loading assets, progress:", ratio);

    // when the ratio equals '1', we are finished.
    if (ratio == 1.0)
        startGame();
});
----

ここの `startGame` メソッドは、自身で実装して下さい。そこの実装ではローディング画面を消して、実際のゲームを開始できます。

`verbose` プロパティが有効になっていると、アクセスされたアセットの名前をログ画面で確認する事ができます。

----
[AssetManager] Adding sound 'explosion'
[AssetManager] Adding texture 'bird'
[AssetManager] Adding texture 'atlas'
[AssetManager] Adding texture atlas 'atlas'
[AssetManager] Removing texture 'atlas'
----

[NOTE]
====
テクスチャを作成した後の最後の行で `atlas` テクスチャーが削除されている事に気づいたでしょうか？
何故でしょう？

一度アトラスが作成されると、アトラステクスチャ自体には用がなくなります。それに含まれているサブテクスチャだけが必要です。
したがって、実際のアトラステクスチャは削除されます。他のテクスチャのために空きを作るのです。
これと同じ事がビッットマップフォンとでも行われます。
====

=== アセットへのアクセス

キューの読み込みが終了しました。AssetManager の、さまざまな `get...` で始まるメソッドで読み込んだファイルにアクセスする事ができます。
それぞれのアセットは名前によって参照可能で、拡張子なしのファイル名か、ソースに埋め込まれたクラスの名前となっています。

[source, as3]
----
var texture:Texture = assets.getTexture("bird"); // <1>
var textures:Vector.<Texture> = assets.getTextures("animation"); // <2>
var explosion:SoundChannel = assets.playSound("explosion"); // <3>
----
<1> この記述では、テクスチャ、次にテクスチャアトラス、の順序でファイルが探されます。
<2> 上と同じですが、引数の文字列で始丸名前を持つ全てのテクスチャ/サブテクスチャが返されます。
<3> サウンドが再生され、それをコントロールする _SoundChannel_ が戻り値で戻されます。

もしもビットマップフォントを読み込ませているなら、すでに利用可能な状態となっています。

[TIP]
====
自分がゲームを作る場合は、AssetManager をルートクラスの `static` プロパティとして保持しています。
これによって、ゲームプログラムのどこからで各アセットにとても簡単に参照できます。
やり方はシンプルに `Game.assets.get...()` と、するだけです。（ルートクラス名が `Game` だとした場合）
====

// TODO: Restoring the Assets

