== メッシュ

//原文：_Mesh_ is the basic building block of all tangible display objects.
_Mesh_ は全てのディスプレイオブジェクトの基本構成要素です。

//原文：With "tangible", I mean a "leaf" object in the display list: an object that is not a container but is rendered directly to the back buffer.
ここで言うディスプレイオブジェクトは、ディスプレイリスト内の葉要素であるオブジェクトの事を指しています。
つまり、スプライトなどのコンテナではなく、実際に画面に表示される部分となるオブジェクトです。

このメッシュクラスはとても重要です。詳細を確認してみましょう。

//原文：In a nutshell, a _Mesh_ represents a list of triangles to be rendered via _Stage3D_.
_Mesh_ とは _Stage3D_ 上で描画される三角形の集合の事です。

//原文：It was mentioned a few times already, since it's the base class of _Quad_ and _Image_.
すでに何度か述べましたように、 _Mesh_ は _Quad_ や _Image_ のベースクラスです。
継承ツリーを再度下記に図示します。

[plantuml, mesh-classes-from-display-object]
....
include::../defaults.plantuml[]

DisplayObject <|-- Mesh
Mesh <|-- Quad
Quad <|-- Image
....

_Mesh_ は抽象クラスではありません。実際にインスタンス化する事が可能です。

[source, as3]
----
var vertexData:VertexData = new VertexData();
vertexData.setPoint(0, "position", 0, 0);
vertexData.setPoint(1, "position", 10, 0);
vertexData.setPoint(2, "position", 0, 10);

var indexData:IndexData = new IndexData();
indexData.addTriangle(0, 1, 2);

var mesh:Mesh = new Mesh(vertexData, indexData);
addChild(mesh);
----

このように、まずメッシュより先に別の２つのクラスをインスタンス化する必要があります。
_VertexData_ と _IndexData_ です。
//原文：They represent collections of vertices and indices, respectively.
それぞれ、頂点情報とindex情報を保持します。

* _VertexData_ は頂点のそれぞれの位置や色など、属性情報を効率的に保持します。
* _IndexData_ はその頂点のindex番号を保持します。３つのindex情報で三角形が作られます。

そのようにして、このコードでは最も基本的な描画オブジェクトである三角形を作りあげます。
３つの頂点情報を時計回りで参照しながら描画を行います。
//原文：After all, that's what a GPU can do best: drawing triangles -- lots of them.
結局、GPUがうまくできる事はたくさんの三角形を描画する事なのです。

.上記で作成された三角形。
image::triangle.png[Triangle, 180]

=== メッシュの継承

_VertexData_ と _IndexData_ を直接扱っていくのは、かなり面倒です。
//原文：It makes sense to encapsulate the code in a class that takes care of setting everything up.
これらの扱いは何かのクラスに処理を隠蔽してしまうのが良いやり方でしょう。

ここで、どのようにカスタムメッシュを作るのかを説明するために、
_NGon_ という簡単なクラスを作ることにします。
N角形のポリゴンを好きな色で描画する事がこのクラスの仕事です。

.下記は描画させたい形状の例です。
image::polygons.png[Polygons]

そのクラスには通常のディスプレイオブジェクトと同じように動作しいと思います。
インスタンス化でき、好きな位置に移動でき、ディスプレイリストに追加できるのです。

[source, as3]
----
var ngon:NGon = new NGon(100, 5, Color.RED); // <1>
ngon.x = 60;
ngon.y = 60;
addChild(ngon);
----
<1> コンストラクタの引数はそれぞれ、半径、エッジの数、色、を示します。

この機能をどのように実現すればいいでしょうか。

=== 頂点の設定

//原文：Like all other shapes, our regular polygon can be built from just a few triangles.
他の全てのシェイプと同じように、このポリゴン形状もいくつかの三角形から構成されています。
下記は五角形をどのように三角形で構成させるかの図です。（N が 5 である NGon が五角形です。）

.五角形と頂点。
image::pentagon.png[Pentagon, 200]

この五角形は６つの頂点から作られた、５つの三角形で構成されています。
それぞれの頂点には 0 ~ 5 の番号を与え、真ん中のものを 5 とします。

先ほどに説明したように、頂点情報は _VertexData_ インスタンスに保持されます。
_VertexData_ は各頂点ごとに、いくつかの属性値を持ちます。
ここでは、２つの標準的な属性値を必要とします。

* `position` 属性が２次元座標の点、x, y を保持します。
* `color` 属性が RGBA 形式の色情報を保持します。

_VertexData_ クラスはそれらの属性値にアクセスするいくつかのメソッドを持っています。
そのメソッド経由でポリゴン形状の頂点を設定する事ができます。

_Mesh_ クラスを継承して、新たに _NGon_ クラスを作ります。
そして下記のようなメソッドを定義します。

[source, as3]
----
private function createVertexData(
    radius:Number, numEdges:int, color:uint):VertexData
{
    var vertexData:VertexData = new VertexData();

    vertexData.setPoint(numEdges, "position", 0.0, 0.0); // <1>
    vertexData.setColor(numEdges, "color", color);

    for (var i:int=0; i<numEdges; ++i) // <2>
    {
        var edge:Point = Point.polar(radius, i*2*Math.PI / numEdges);
        vertexData.setPoint(i, "position", edge.x, edge.y);
        vertexData.setColor(i, "color", color);
    }

    return vertexData;
}
----
<1> 真ん中の頂点を設定します。(最後の index 番号。)
<2> エッジ部分の頂点を設定します。

ここでのメッシュは単色で構成させるため、全ての頂点に同じ色を設定しています。
各エッジ（コーナー）の頂点座標はある半径の円周に沿って設定されます。

=== Index の設定

//原文：Now we need to define the triangles that make up the polygon.
頂点についてはここまでとし、次は、ポリゴン形状を構成する三角形を設定します。

//原文：_Stage3D_ wants a simple list if indices, with each three successive indices referencing one triangle.
３つの連続した頂点の index 番号で１つの三角形を表す事ができますが、_Stage3D_ は、その頂点の組が羅列して並んたデータを必要とします。
頂点は時計回りで順番に参照するのが良いでしょう。
//原文：that convention indicates that we are looking at the front side of the triangle.
そのルールで並んだ頂点で作られる三角形は、こちら側におもて面を向けます。
よって、五角形の indices データは下記のような内容となります。

    5, 0, 1,   5, 1, 2,   5, 2, 3,   5, 3, 4,   5, 4, 0

Starling では、このような頂点番号のリストを設定するために _IndexData_ クラスを使います。
下記のメソッドで、適切な index 番号を用いて _IndexData_ インスタンスを設定します。

[source, as3]
----
private function createIndexData(numEdges:int):IndexData
{
    var indexData:IndexData = new IndexData();

    for (var i:int=0; i<numEdges; ++i)
        indexData.addTriangle(numEdges, i, (i+1) % numEdges);

    return indexData;
}
----

=== NGon コンストラクタ

以上が、実際に _NGon_ クラスに必要な処理の全てです。
上記メソッドをコンストラクタ内で使用してやります。

ディスプレイオブジェクトとして必要なその他の基本処理
（あたり判定、描画処理、バウンズの計算、など）
は親クラスが処理してくれます。

[source, as3]
----
public class NGon extends Mesh
{
    public function NGon(
        radius:Number, numEdges:int, color:uint=0xffffff)
    {
        var vertexData:VertexData = createVertexData(radius, numEdges, color);
        var indexData:IndexData = createIndexData(numEdges);

        super(vertexData, indexData);
    }

    // ...
}
----

簡単ですよね？
//原文：This approach works for any shape you can think of.
このやり方は他のどんな形状のシェイプにも用いる事ができます。

[TIP]
====
カスタムメッシュを扱う際は、`starling.geom` パッケージに存在する _Polygon_ クラスもチェックしてみてください。

このクラスは、いくつかの頂点で定義される任意の閉じた形状のシェイプを三角形で分割する際に参考になります。
詳細は <<Masks>> セクションでさらに解説します。
====

=== テクスチャの追加

このポリゴンにテクスチャを適用できたら良いですね。
実は、ベースクラスの _Mesh_ は、すでに `texture` プロパティを持っています。
まだ _テクスチャの座標設定_ を行なっていないだけです。

テクスチャの座標設定を行うことによって、テクスチャのどの部分を頂点に適用するかが決定されます。
//原文：which is a reference to the names that are typically used for their coordinate axes (_u_ and _v_).
//うまく訳せない。。
一般的に、_u値、v値_ で表される座標軸でテクスチャの場所を示すので、しばしば _UV座標_ と呼ばれます。

UVの値の範囲は、テクスチャの実際の大きさに関わらず、_0 から 1_ の間に限定される事に注意してください。

.ポリゴンのテクスチャの座標は 0~1 の範囲で表されます。
image::pentagon-texcoords.png[Pentagon Texture Coordinates, 250]

これに従い、`createVertexData` メソッドを適切に調整します。

[source, as3]
----
function createVertexData(
    radius:Number, numEdges:int, color:uint):VertexData
{
    var vertexData:VertexData = new VertexData(null, numEdges + 1);
    vertexData.setPoint(numEdges, "position", 0.0, 0.0);
    vertexData.setColor(numEdges, "color", color);
    vertexData.setPoint(numEdges, "texCoords", 0.5, 0.5); // <1>

    for (var i:int=0; i<numEdges; ++i)
    {
        var edge:Point = Point.polar(radius, i*2*Math.PI / numEdges);
        vertexData.setPoint(i, "position", edge.x, edge.y);
        vertexData.setColor(i, "color", color);

        var u:Number = (edge.x + radius) / (2 * radius); // <2>
        var v:Number = (edge.y + radius) / (2 * radius);
        vertexData.setPoint(i, "texCoords", u, v);
    }

    return vertexData;
}
----
//原文:<1> The texture coordinates of the center vertex: `0.5, 0.5`.
<1> 真ん中の頂点のテクスチャ座標を `0.5, 0.5` といします。
//原文：<2> The origin of the n-gon is in the center, but the texture coordinates must be all positive.
<2>元々 n-gon は真ん中 (0,0) に位置しているのですが、テクスチャ座標系ではそれぞれの値が必ず正の値とならなくてはいけません。
//原文：So we move the vertex coordinates to the right (by `radius`) and divide them by `2 * radius` to end up in the range `0-1`.
そのため、頂点座標を半径の分だけ右に移動し、その値を `2 * radius` で除算します。値を 0 ~ 1 の範囲に納めるためです。
//原文：When a texture is assigned, the rendering code will automatically pick up those values.
テクスチャがこのように割り当てられると、レンダリング処理に際に自動的にそれらの設定が参照されます。

[source, as3]
----
var ngon:NGon = new NGon(100, 5);
ngon.texture = assets.getTexture("brick-wall");
addChild(ngon);
----

.テクスチャが設定された五角形です。
image::pentagon-textured.png[Textured Pentagon, 230]

=== アンチエイリアス処理

NGonのエッジをよく観察してみると、ジャギーが発生している事に気づくかもしれません。
これは、GPU がピクセルを n-gon の描画領域の内側か外側かどうかしか判定せず、その中間の状態が存在しないからです。
これを改善するために、アンチエイリアス処理を有効にする事ができます。_Starling_ クラスのプロパティにそのままの名前でプロパティが存在します。

[source, as3]
----
starling.antiAliasing = 4;
----

//原文：The value correlates to the number of subsamples _Stage3D_ uses on rendering.
この値は _Stage3D_ がレンダリングをする際に利用するサブサンプリングの回数に関連します。
//原文：Using more subsamples requires more calculations to be performed, making anti-aliasing a potentially very expensive option.
たくさんのサブサンプリングを行うために、たくさんの計算が発生します。アンチエイリアス処理は高コストな処理なのです。
さらに言えば、_Stage3D_ は全てのプラットフォームでアンチエイリアス処理をサポートしていません。

NOTE: モバイル環境では、アンチエイリアス処理は _RenderTextures_ 内だけで利用が可能です。

よって、上記のアンチエイリアス処理は現実的なジャギーの解決方法ではありません。
//原文：The only consolation I can offer:
幸いなことに、スクリーンのピクセル密度は一般的にどんどんと上昇しています。
近年のハイエンドなモバイル端末では１ピクセルの大きさがとても小さいため、もはやジャギーが目立たず、あまり問題とはならないのです。

.アンチエイリアス処理はピクセルのジャギーをスムースにする事ができます。
image::anti-aliasing.png[Anti-Aliasing, 380]

// TODO: describe RenderTexture-workaround in "Pro-Tips" chapter and link to it here.

=== メッシュスタイル

ここまでで、任意の形状のシェイプにテクスチャを適用する方法はわかりました。
その方法では、Starling に搭載されたスタンダードなレンダリング方法を用いています。

さてでは、レンダリングのやり方そのものをカスタマイズしたい場合はどうしたら良いでしょう？
//原文：The properties and methods of the _Mesh_ class provide a solid foundation -- but sooner or later, you will want more than that.
_Mesh_ クラスのメソッドとプロパティは手堅い基本的な機能を提供してくれますが、遅かれ早かれその機能だけでは物足りなくなってくる事でしょう。
//原文：Coming to the rescue: Starling's _mesh styles_.
Starling の _メッシュスタイル_ がこの要望の解決を手助けします。

//原文：Styles are a brand new addition to Starling (introduced in version 2.0)
//and are the recommended way to create custom, high performance rendering code.
`スタイル` は Starling2.0 で導入された新機能で、カスタムレンダリング処理を作成するための推奨される方法です。スタイルの処理は高速な描画が可能です。
//原文：In fact, all rendering in Starling is now done via mesh styles.
実際、Starling 内の全ての描画処理は、メッシュスタイルを通して実行されています。

* どの _Mesh_ (及び _Mesh_のサブクラスのインスタンス)にもスタイルを１つ設定する事ができます。
* デフォルトでは、それぞれのメッシュに紐づいているのは、 _MeshStyle_ 基底クラスのインスタンスです。
* このスタイルは Starling の標準的な描画能力を提供します。三角ポリゴンに色味とテクスチャを加えて描画します。

//原文：To teach your meshes new tricks, you can extend _MeshStyle_.
メッシュに新しい見たの効果を加えるには、_MeshStyle_ を継承する方法が良いです。
//原文：This allows you to create custom shader programs for all kinds of interesting effects.
カスタムのシェーダープログラムを作れば、様々なエフェクトを実現することができます。
例えば、複数のテクスチャの同時割り当てや、カラーの高速トランジションなどが実装可能です。

[TIP]
====
スタイルの印象的な例として、 http://wiki.starling-framework.org/extensions/dynamic_lighting[Dynamic Lighting extension] をあげておきます。
ノーマルマップ（ポリゴン面の法線方向をエンコードしたテクスチャ）を用いて、リアルな照明効果をダイナミック(動的)に適用することができます。
Starling Wiki で 実際にこの拡張機能が動いている様子を確認してみてください。
====

スタイルを実際に使うには、まずそのインスタンスを生成し、メッシュの `style` プロパティとして設定します。

[source, as3]
----
var image:Image = new Image(texture);
var lightStyle:LightStyle = new LightStyle(normalTexture);
image.style = lightStyle;
----

.照明処理のスタイルが動的に動いている様子。
image::dynamic-lighting.png[Dynamic Lighting]

スタイルで実現できることは本当にたくさんあり、適用範囲にほとんど限界がありません。

//原文：And since meshes with the same style can be batched together, you do not sacrifice performance in any way.
さらに、同じスタイルが設定されているメッシュは、バッチ処理で同時に処理されるので、パフォーマンスを気にする必要もあまりありません。
この点に関して、同じような目的で用いられるフラグメントフィルターよりもずっと効果的にスタイルを利用する事ができます。

スタイル短所には、それがメッシュだけに適用でき、スプライトには適用できないという点があります。
また、スタイルはメッシュの描画領域内のみで有効となります。描画領域をはみ出して効果を与える'ぼかし'効果のような事はできないのです。
一つのメッシュに対して複数のスタイルを組み合わせて使う事もできません。

それでも、スタイルは Starling 開発者全員が身につけて使うべきパワフルなツールです。
//原文：Stay tuned: in a <<Custom Styles, later section>>, I'll show you how to create your own mesh style from scratch, shaders and all!
この後に続く <<Custom Styles>> のセクションも確認してください。
スクラッチでカスタムスタイルを作成する方法や、その他シェーダーなどについて解説します。

[NOTE]
====
もしまだ、 _メッシュ_ と _メッシュスタイル_ の違いについてよく理解できないのであれば、このように考えてみてください。
_メッシュ_ は頂点の集合でしかなく、頂点でどのように三角形を描くかを示しているだけ、であると。

スタイルは各頂点に追加で情報を設定し、それをレンダリング時に使用します。
標準の _MeshStyle_ はカラーとテクスチャの割り当てを行います。
_MultiTextureStyle_ は追加のテクスチャ割り当て情報などを制御するかもしれません。
しかし、スタイルは決して元々のオブジェクト形状を変更しません。
スタイルは、頂点を足しも減らしもせず、位置の移動も行わないのです。
====
