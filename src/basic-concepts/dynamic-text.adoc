== ダイナミックテキスト
ifndef::imagesdir[:imagesdir: ../../img]

どのアプリケーションでもテキストの表示は大事な要素です。
多量の情報はイメージを通してのみ伝える事ができますが、そのうちいくらかは言葉としてランタイムで動的に描画されます。
//なんか翻訳辛い 原文：You can only convey so much information with images; some things simply need to be described with words, dynamically at run-time.

=== テキストフィールド

Starling では、動的なテキスト（ダイナミックテキスト）を簡単に表示する事ができます。
_TextField_ クラスについては説明するまでもないかもしれません。
//原文：The _TextField_ class should be quite self explanatory!

[source, as3]
----
var textField:TextField = new TextField(100, 20, "text"); // <1>
textField.format.setTo("Arial", 12, Color.RED); // <2>
textField.format.horizontalAlign = Align.RIGHT; // <3>
textField.border = true; // <4>
----
<1> 100 × 20 ポイントのサイズでテキストフィールドを作成し、"text" という文字を表示しています。.
<2> テキストの装飾は "Arial" フォント、12 ポイント、赤、に設定されています。
<3> テキストが右寄せに設定されています。
<4> borderの設定は開発時に役に立ちます。テキストフィールドの周りに枠線が表示されます。

NOTE: テキストの装飾は `format` プロパティを通して設定されます。`starling.text.TextFormat` のインスタンスです。

一度 _TextField_ が生成されれば、その後はイメージや Quad と同じように扱う事ができます。

.Starling の テキスト表示機能のいくつかの例。
image::textfield-samples.png[TextField Samples, 320]

=== TrueType フォント

デフォルトでは、Starling はシステムフォントをテキストのレンダリングに利用します。
例えば、"Arial" フォント をテキストフィールドに適用した場合、システムにインストールされたものを使います。（もしインストールされていれば。）

しかし、そのやり方ではレンダリングクオリティがあまりよくなりません。例えば、アンチエイリアス処理が行われず。フォントがレンダリングされてしまったりします。
より良い出力を得るためには、_TrueType_ フォントを直接 swf ファイルに埋め込みます。
下記のコードに従ってください。

[source, as3]
----
[Embed(source="Arial.ttf", embedAsCFF="false", fontFamily="Arial")]
private static const Arial:Class; // <1>

[Embed(source="Arial Bold.ttf", embedAsCFF="false", fontFamily="Arial", fontWeight="bold")]
private static const ArialBold:Class; // <2>

[Embed(source="Arial Italic.ttf", embedAsCFF="false", fontFamily="Arial", fontStyle="italic")]
private static const ArialItalic:Class; // <3>

[Embed(source="Arial.ttf", embedAsCFF="false", fontFamily="Arial", unicodeRange = "U+0020-U+007e")]
private static const ArialJustLatin:Class; // <4>
----
<1> 標準的な _Arial_ フォントを埋め込みます。_embedAsCFF_ という項目の記述を忘れないでください。記述がないとフォントが表示されなくなります。
<2> Bold と italic スタイルではそれぞれ別の埋め込み指定が必要です。_fontWeight_ 属性に注目してください。
<3> こちらには _fontStyle_ 属性があります。
<4> どの文字を含めるか指定する事もできます。この機能はフォントデータの文字の一部しか必要がない場合に便利です。
この例で指定しているのは 基本的な Latin の範囲（英数字の大文字小文字と一般的な記号と句読点）です。

フォントを埋め込んだ後は、対応したフォント名（もしくはフォントファミリー）とウェイトをテキストフィールドに設定するだけで利用する事ができます。
そのほかの設定・準備は必要ありません。

TIP: フォントのすべての文字を埋め込んだ場合、とても大きなデータ範囲指定となってしまう事に気をつけてください。
//Beware of the big footprint when embedding all glyphs of a font.
上記のようにユニコードの範囲指定をすると、その問題が軽減します。範囲の設定は、例えばこのサイトで作成する事ができます。
http://renaun.com/blog/2011/10/flash-embed-font-unicode-range-generator[Unicode Range Generator].

TIP: もしもフォントの表示にかけが生じたり、正しい位置に表示されないような場合は、`stage.quality` 設定を確認してくみてださい。
低いクオリティ設定の場合、テキストの大きさを Flash/AIR が正しく認識できない事があります。Starling はその値を元に描画を行なっています。
（Flash のステージについての話です。そしてこの問題は TrueType フォントだけに起こります。）

=== ビットマップフォント

_TrueType_ フォントを使うのは、あまりテキストの内容に変更がない場合に適しています。
内容が頻繁に変更される場合、もしくは TrueType フォントでは再現できないような見た目が豪華な装飾付きのフォントを表示したい場合には、ビットマップフォントを代わりに使うべきです。

ビットマップフォントは、表示したい全ての文字を含ませたテクスチャーと、
テクスチャーアトラスと同じように、テクスチャー内の文字の位置情報を保持したXML ファイルからなります。

それだけ揃えれば Starling でビットマップフォントを表示する事ができます。
それらのファイルを用意するには、いくつかの方法があります。

* http://kvazars.com/littera/[Littera], フリーで高機能なオンライン上のビットマップフォント作成ツールです。
* http://www.angelcode.com/products/bmfont/[Bitmap Font Generator], AngelCode から提供されている TrueType フォントからビットマップフォントを生成するツールです。Windows 専用です。
* http://glyphdesigner.71squared.com[Glyph Designer] 豪華なエフェクトをフォントに加える事もできるツールです。_macOS_ 用。
* http://www.bmglyph.com[bmGlyph], こちらも _macOS_専用。App Store で入手できます。

どのツールも使い方は似ています。システムフォントから使いたい文字を選択し、文字にエフェクトを施す事ができます。

フォントの書き出しにあたって、考慮すべき点があります。

* Starling は 独自 XML 形式の ".fnt" ファイルを必要とします。
//原文：* Starling requires the XML variant of the ".fnt" format.
* 必要な文字だけを正しく選択するようにしましょう。必要ないものも含めると、フォントのテクスチャーが巨大なものになります。

書き出されるのは、".fnt" ファイルと、選択した文字を含んだテクスチャーです。

.このビットマップフォントには色がついており、シャドウの装飾も含希ています。
image::desyrel-font.png[Bitmap Font, 400]

Starling で、フォントを表示するには、SWF にそれらのファイルを埋め込んで、テキストフィールドクラスに登録します。

[source, as3]
----
[Embed(source="font.png")]
public static const FontTexture:Class;

[Embed(source="font.fnt", mimeType="application/octet-stream")]
public static const FontXml:Class;

var texture:Texture = Texture.fromEmbeddedAsset(FontTexture);
var xml:XML = XML(new FontXml());
var font:BitmapFont = new BitmapFont(texture, xml); // <1>

TextField.registerCompositor(font); // <2>
----
<1> _BitmapFont_ クラスをインスタンス化します。
<2> _TextField_ クラスにそのインスランスを登録します。

ビットマップフォントが _TextField_ クラスに一度登録されると、その後はそのインスタンスは必要無くなります。
Starling はビットマップフォントの名前が指定された _TextField_ を見つけると、自動的に対応したフォントを利用します。
以下のような感じです。

[source, as3]
----
var textField:TextField = new TextField(100, 20, "Hello World");
textField.format.font = "fontName"; // <1>
textField.format.fontSize = BitmapFont.NATIVE_SIZE; // <2>
----
<1> 使いたいフォントの名前を指定をします。デフォルトでは、XML ファイル内の `face` 属性で、フォント名が設定された物が使われます。
<2> テクスチャーを作成する時と同じフォントサイズでフォントが表示される際に、最もビットマップフォントが美しく描画されます。
手作業でそのサイズを設定する事もできますが、`NATIVE_SIZE` という定数指定で Starling にそれを自動で行わせた方がスマートです。

==== 了解事項
//原文。。：==== Gotchas

もう一つ、知っておかなくてはいけない事があります。もしもビットマップフォントのカラー設定が単色だった場合（カラーエフェクトなしの通常の truetype フォントだった場合）、フォント形状は白一色で書き出される必要があります。
そのようになっていれば、テキストフィールドの `format.color` プロパティを用いてフォント表示を着色する事ができます。
//原文：The `format.color` property of the TextField can then be used to tint the font into an arbitrary color at runtime
（テクスチャの RGB チャンネルに乗算処理がかかるだけのシンプルな処理がなされます。）

一方で、フォントに（上の画像の例のように）カラー情報が含まれている場合、テキストフィールドの `format.color` プロパティは白 (`Color.WHITE`).に設定されるべきです。
その設定であれば、テキストフィールドのカラー設定がテクスチャカラーに影響を与える事はありません。

TIP: パフォーマンス向上のため、フォントテクスチャーもテクスチャーアトラスに含める事ができます。
１毎のテクスチャーアトラスにまとまっていれば、テキストの表示も他のイメージとともにバッチされる事があります。結果ドローコールの削減となります。

==== MINI フォント

Starling ライブラリには、とても軽量なフォントが最初から含まれています。
美しさで賞される事はないかもしれませんが、開発中やデバッグ用途のテキスト表示用途にはもってこいです。

."MINI" ビットマップフォント。
image::mini-font.png[BitmapFont.MINI, 396]

"軽量" と書いた通り、それぞれの文字は 5 ピクセルの高さしか持たないのです。
実際に表示される時は、200% の大きさで拡大表示して使われます。

[source, as3]
----
var textField:TextField = new TextField(100, 10, "The quick brown fox ...");
textField.format.font = BitmapFont.MINI; // <1>
textField.format.fontSize = BitmapFont.NATIVE_SIZE * 2; // <2>
----
<1> MINI フォントの利用。
<2> 元の大きさの２倍とします。このフォントはニアレスネイバー法で拡大されるため、くっきりとした表示が保たれます！
