== ダイナミックテキスト
ifndef::imagesdir[:imagesdir: ../../img]

そのアプリケーションにもテキスト表示は大事な項目です。
たくさんの情報はイメージを通してのみ伝える事ができます。そのうちいくらかは言葉としてランタイム時に動的に描画されます。
//なんか翻訳辛い 原文：You can only convey so much information with images; some things simply need to be described with words, dynamically at run-time.

=== テキストフィールド

Starling では動的なテキスト（ダイナミックテキスト）を簡単に表示できます。
_TextField_ クラスについては説明するまでもないかもしれません。
//原文：The _TextField_ class should be quite self explanatory!

[source, as3]
----
var textField:TextField = new TextField(100, 20, "text"); // <1>
textField.format.setTo("Arial", 12, Color.RED); // <2>
textField.format.horizontalAlign = Align.RIGHT; // <3>
textField.border = true; // <4>
----
<1> 00×20 ポイントのサイズでテキストフィールドを作成し、"text" という文字を表示しています。.
<2> テキストの装飾を "Arial" フォント、12 ポイントのサイズ、赤い色、と設定しています。
<3> テキストが右寄せに設定されています。
<4> borderの設定は開発時に役に立つ事が多いです。テキストフィールドの周りに枠線が表示されます。

NOTE: テキストの装飾は `format` プロパティを通して設定されます。`starling.text.TextFormat` のインスタンスがその値となります。

一度 _TextField_ が生成されれば、そのあとはイメージや Quad と同じように扱う事ができます。

.Starling の テキスト表示機能に関するいくつかの例。
image::textfield-samples.png[TextField Samples, 320]

=== TrueType フォント

デフォルトでは、Starling はシステムフォントをテキストのレンダリングに利用します。
例えば、"Arial" フォント をテキストフィールドに適用した場合、システムにインストールされたそれを使います。（もしインストールされているならば。）

しかし、そのやり方でのレンダリングクオリティは最適ではありません。例えば、アンチエイリアス処理なしでフォントがレンダリングされてしまったりします。
より良い出力のためには、_TrueType_ フォントを直接 swf ファイルに埋め込む事になります。
下記のコードに従ってください。

[source, as3]
----
[Embed(source="Arial.ttf", embedAsCFF="false", fontFamily="Arial")]
private static const Arial:Class; // <1>

[Embed(source="Arial Bold.ttf", embedAsCFF="false", fontFamily="Arial", fontWeight="bold")]
private static const ArialBold:Class; // <2>

[Embed(source="Arial Italic.ttf", embedAsCFF="false", fontFamily="Arial", fontStyle="italic")]
private static const ArialItalic:Class; // <3>

[Embed(source="Arial.ttf", embedAsCFF="false", fontFamily="Arial", unicodeRange = "U+0020-U+007e")]
private static const ArialJustLatin:Class; // <4>
----
<1> 標準的な _Arial_ フォントを埋め込みます。_embedAsCFF_ という部分の記述を忘れないでください。さもないとフォントが表示されなくなります。
<2> Bold 及び italic スタイルは別々の埋め込みが必要です。_fontWeight_ 属性に注目してください。
<3> こちらは _fontStyle_ 属性があります。
<4> どの文字を含めるか指定する事もできます。この機能は大きなフォントデータの一部の文字しか必要がない場合に便利です。ここで指定している範囲は 基本的なLatinの範囲です。（英数字の大文字小文字と一般的な記号と句読点。）

フォントを埋め込んだ後は、テキストフィールドに対応したフォント名（もしくはフォントファミリー）とウェイトを設定するだけで利用する事ができます。
そのほかの設定や準備は必要ありません。

TIP: フォントのすべての文字を埋め込んだ場合、とても大きな範囲指定となる事に気をつけてください。
//Beware of the big footprint when embedding all glyphs of a font.
上記のユニコードの範囲指定はその問題を軽減します。範囲設定は例えばこのサイトで作成する事ができます。 http://renaun.com/blog/2011/10/flash-embed-font-unicode-range-generator[Unicode Range Generator].

TIP: もしもフォント表示にかけが出たり正しい位置に表示されない場合は、`stage.quality` 設定を確認してください。
低いクオリティ設定である場合、テキストの大きさを Flash/AIR が正しく認識しない事があります。Starling はその値を元に描画を行なっています。
（Flash のステージについての話です。そしてこの問題は TrueType フォントだけに起こります。）

=== ビットマップフォント

上記のように _TrueType_ フォントを使う事は、あまりテキストの内容に変更がない場合に適しています。
しかし、内容が頻繁に変更される場合、もしくは TrueType では再現する事ができないような見た目が派手なフォントを表示したい場合には、代わりにビットマップフォントを使うべきです。

ビットマップフォントは表示したい全ての文字を含んでいるテクスチャーです。
テクスチャーアトラスと同じように、XML ファイルがテクスチャー内の文字の位置情報を保持しています。

これだけ揃えれば Starling はビットマップフォントを表示する事ができます。
ファイルを揃えるには、いくつかの方法があります。

* http://kvazars.com/littera/[Littera], フリーで高機能なオンライン上のビットマップフォント作成ツールです。
* http://www.angelcode.com/products/bmfont/[Bitmap Font Generator], AngelCode から提供されている TrueType フォントからビットマップフォントを生成するツールです。Windows 専用です。
* http://glyphdesigner.71squared.com[Glyph Designer] 豪華なエフェクトをフォントに加える事もできるツールです。_macOS_ 用。
* http://www.bmglyph.com[bmGlyph], こちらも _macOS_専用。App Store で入手できます。

どのツールも使い方は似ています。システムフォントから使いたいものを選択し、エフェクトを施す事もできます。

フォントの書き出しに当たって、考慮すべき点があります。

* Starling は 独自の XML 形式の ".fnt" ファイルを必要とします。
//原文：* Starling requires the XML variant of the ".fnt" format.
* 必要な文字だけを正しく選択するようにしましょう。必要ないものも含めると、フォントのテクスチャーが巨大なものになります。

掻き出されるのは、".fnt" ファイルと、選択した文字を含んだテクスチャーです。

.このビットマップフォントは、色がついており、シャドウ部分も含んでいます。
image::desyrel-font.png[Bitmap Font, 400]

Starling で、フォントを表示するには、SWF にそれらのファイルを SWF に埋め込んで、テキストフィールドクラスに登録します。

[source, as3]
----
[Embed(source="font.png")]
public static const FontTexture:Class;

[Embed(source="font.fnt", mimeType="application/octet-stream")]
public static const FontXml:Class;

var texture:Texture = Texture.fromEmbeddedAsset(FontTexture);
var xml:XML = XML(new FontXml());
var font:BitmapFont = new BitmapFont(texture, xml); // <1>

TextField.registerCompositor(font); // <2>
----
<1> _BitmapFont_ クラスをインスタンス化します。
<2> _TextField_ クラスにそのインスランスを登録します。

一度ビットマップフォントが _TextField_ クラスに登録されると、その後そのインスタンスは必要となりません。
Starling は登録されたビットマップフォントの名前が指定された _TextField_ を見つけると、自動的に対応したフォントを利用します。
以下のような感じです。

[source, as3]
----
var textField:TextField = new TextField(100, 20, "Hello World");
textField.format.font = "fontName"; // <1>
textField.format.fontSize = BitmapFont.NATIVE_SIZE; // <2>
----
<1> 使いたいフォントの名前を指定をします。デフォルトでは、XML ファイル内の `face` 属性で、フォント名が設定されています。
<2> テクスチャーを作成する時に指定したフォントサイズと同じサイズで表示されルト、最もビットマップフォントが美しく描画されます。
マニュアル作業でそのサイズに設定する事もできますが、`NATIVE_SIZE` という指定で Starling にそれを行わせた方が良いでしょう。

==== 了解事項
//原文。。：==== Gotchas

もう一つ、知っておかなくてはいけない事があります。もしもビットマップフォントのカラー設定が単色だった場合（カラーエフェクトなしの通常の truetype フォントだった場合）、フォント画像は白一色で書き出される必要があります。
そうすれば、テキストフィールドの `format.color` プロパティを用いてフォントを動的に着色する事ができます。
//原文：The `format.color` property of the TextField can then be used to tint the font into an arbitrary color at runtime
（テクスチャの RGB チャンネルにシンプルに乗算処理がかかるだけの処理とされます。）

一方で、フォントが上の画像にあるように、カラー情報を含んでいる場合、テキストフィールドの `format.color` プロパティは白 (`Color.WHITE`).に設定されるべきです。
そのようにすれば、テキストフィールドのカラー設定がテクスチャカラーに影響を与える事はありません。

TIP: パフォーマンス向上のため、フォントテクスチャーもテクスチャーアトラスに含める事ができます。そうする事で、テキスト表示も他のイメージとともにバッチされる事があります。結果ドローコールの削減となります。

==== MINI フォント

Starling には最初からとても軽量なフォントが含まれています。
美しさで賞される事はないかもしれません。しかし、開発中やデバッグ用途のテキスト表示にはもってこいです。

."MINI" ビットマップフォント。
image::mini-font.png[BitmapFont.MINI, 396]

"軽量" と言いましたが、それぞれの文字は 5 ピクセルの高さしか持たないのです。
実際に表示される際には、200% の大きさで拡大して使います。

[source, as3]
----
var textField:TextField = new TextField(100, 10, "The quick brown fox ...");
textField.format.font = BitmapFont.MINI; // <1>
textField.format.fontSize = BitmapFont.NATIVE_SIZE * 2; // <2>
----
<1> MINI フォントの利用。
<2> 元の大きさの２倍とします。このフォントはニアレスネイバー法で拡大されるため、くっきりとした表示が保たれます！
