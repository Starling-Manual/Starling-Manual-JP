== アニメーション

アニメーションはゲームの重要な要素であるというだけでなく、最近ではビジネス向けのアプリであってもスムーズなエフェクトがある事を期待されています。
適切に利用されたアニメーションは、直感的で相互的なインターフェースを提供するために大いに役立ちます。
//原文：Some well placed animations go a long way towards providing a responsive and intuitive interface.
そのような需要を満たせるように、Starling は柔軟なアニメーションエンジンを提供します。

アニメーションには２つのタイプがあります。
//原文：If you think about it, there are two types of animations.

* 一方のタイプのアニメーションはとても動的なので、前もって何が起こるかを知る事は出来ないとわかります。
//原文：On the one hand, you've got animations that are so dynamic that you don't know beforehand what exactly will happen.
自キャラに向かってやって来る敵キャラについて考えてみてください。毎フレーム移動の方向とスピードを状況に合わせて更新しなくてはいけません。
物理演算もそうです。力の追加やあたり判定の存在が、全てを変えてしまいます。
//原文：Or physics: each additional force or collision changes everything.

* 他方で、詳細な計画通りに動作左折事ができるアニメーションもあります。
最初の時点で、この後何がどう起こるのかを知っておく事ができるのです。
消えていくメッセージボックスや、シーンのトランジションアニメーションなどを考えてみて下さい。

以下、その２つのタイプのアニメーションを詳しく見ていきます。

=== EnterFrame イベント

いくつかのゲームエンジンでは、_run-loop_ というものが実装されています。
//原文：In some game engines, you have what is called a _run-loop_.
これは終わりのない無限ループ処理で、定期的にシーン上の全ての物体に対して更新処理を行います。

Starling には、ディスプレイリストの構造的な理由から、run loop 処理が適していません。
ゲームをたくさんのディスプレイオブジェクトで構成しているかと思いますが、それぞれのディスプレイオブジェクト自身が時間の経過とともに何をすればいいのか把握しているべきです。
//原文：You separated your game into numerous different custom display objects,and each should know for itself what to do when some time has passed.

そこが、_EnterFrameEvent_ がポイントとなる部分です。_EnterFrameEvent_ により、時間経過とともにディスプレイオブジェクトが自分自身を更新する事が出来るようになります。
ディスプレイリストを構成する全てのディスプレイオブジェクトに 毎フレーム　EnterFrame イベントが送信されます。
下記はそのイベントの利用例です。

[source, as3]
----
public function CustomObject()
{
    addEventListener(Event.ENTER_FRAME, onEnterFrame); // <1>
}

private function onEnterFrame(event:Event, passedTime:Number):void // <2>
{
    trace("Time passed since last frame: " + passedTime);
    bird.advanceTime(passedTime);
}
----
<1> どこでも EnterFrame に対してりすな登録を行うことできます。コンストラクタに書くのも良い選択です。
<2> イベントリスナーでは、このようにイベントを受け取ります。

`onEnterFrame` メソッドは１フレームに１度だけ呼び出されます。
その際、前のフレームから経過した時間を引数として受け取る事ができます。
その情報を元にして、敵キャラや太陽の位置や、その他必要な処理を行う事ができるのです。

このイベントには、隠れた強力な機能があります。それは、異なる処理をその時々で行えるという事です。
//原文：The power behind this event is that you can do completely different things each time it occurs.
現在のゲームの状況に合わせて柔軟な対応する事ができます。
例えば、シンプルな敵AIとして、敵キャラをプレイヤーの方向に移動させる事もできるのです。

=== トゥイーン

次は、事前に手続きして再生するアニメーションについてです。
//原文：Now to predefined animations.

とても一般的ですが、移動、拡大縮小、フェード、といった類いのアニメーションがあります。
//原文：They are very common and have names such as _movement_, _scale_, _fade_, etc.
Starling では、これらのアニメーションの取り組み方はシンプルです。そして同時にとても柔軟でもあります。
基本的にどのオブジェクトのどのプロパティであっても、それが数値 (`Number`、`int`、`uint`) で表されるものであれば、アニメーションさせる事ができます。
それらのアニメーションは _Tween_ と呼ばれるオブジェクトにより表されます。
//原文：Those animations are described in an object called _Tween_.

NOTE: "Tween" の語源は手書きのアニメーションから来ています。 
チーフイラストレーターが動画の大事なコマだけを描き、残りのつなぎ部分 (be**tween**) はチームの別の担当者が描くのです。

.tween 中のそれぞれのフレーム。
image::soccer-tween.png[Soccer Tween]

理屈はこのくらいにして、実際の例を見てみましょう。

[source, as3]
----
var tween:Tween = new Tween(ball, 0.5);

tween.animate("x", 20);
tween.animate("scale", 2.0);
tween.animate("alpha", 0.0);
----

この tween では　'ボール' の位置が　X＝20 、サイズは倍、透明度は 0 に変化するように設定されています。
これらのアニメーションは、0.5 秒をかけて、一斉に進行します。
パラメータそれぞれのトゥイーン開始時の初期値は、単純に現在のプロパティ値となります。

この例では、以下のような事がわかります。

* オブジェクトの任意の値をアニメーションさせる事が可能です。
* いくつかのアニメーションを１つのオブジェクトにまとめて実行させる事ができます。

ところで、拡大縮小とフェードと移動のアニメーションは、しばしば実行されるので、_Tween_ クラスはそれらに専用のメソッドを用意しています。
同じ処理は下記のようにも書けます。

[source, as3]
----
tween.moveTo(20, 0); // "x" and "y" のアニメーション
tween.scaleTo(2);    // "scale" のアニメーション
tween.fadeTo(0);     // "alpha" のアニメーション
----

tween 処理の面白いところは、アニメーションの進み具合をカスタマイズできると言う事です。
例えば、初めはゆっくり動き、だんだん早くなる、と言うような設定にする事ができます。
トランジションのタイプを指定する事で、それが可能です。

.指定可能なトランジションタイプ。 デフォルトの `linear` のグラフは省略されています。
image::transitions.png[Transitions]

下記は、トランジション指定をどのように行うかと、tween が提供するその他いくつかの機能についての解説です。

[source, as3]
----
var tween:Tween = new Tween(ball, 0.5, Transitions.EASE_IN); // <1>
tween.onStart    = function():void { /* ... */ };
tween.onUpdate   = function():void { /* ... */ }; // <2>
tween.onComplete = function():void { /* ... */ };
tween.delay = 2; // <3>
tween.repeatCount = 3; // <4>
tween.reverse = true;
tween.nextTween = explode; // <5>
----
<1> 3番目の引数でトランジションのタイプを指定します。
<2> これらのコールバックはそれぞれ、トゥイーンが始まった時、トゥイーン中の毎フレーム、トゥイーン終了後に呼び出されます。
<3> アニメーションの開始前に2秒間待ちます。
<4> トゥイーンを 3回繰り返します。オプション指定で、行ったり来たりを繰り返すヨーヨースタイルに設定しています。
もしも `repeatCount` をゼロに設定した場合は、トゥイーンが永久に繰り返されます。
<5> 最初のトゥイーンが終了した後に、別のトゥイーン処理を開始します。

さて、これでトゥイーンの設定を終えたのですが、まだ何も起きません。
tween オブジェクトはアニメーションを設定しますが、実行はしないのです。

tween の `advanceTime` メソッドを手動で実行する事で、アニメーションを再生する事ができます。

[source, as3]
----
ball.x = 0;
tween = new Tween(ball, 1.0);
tween.animate("x", 100);

tween.advanceTime(0.25); // -> ball.x =  25
tween.advanceTime(0.25); // -> ball.x =  50
tween.advanceTime(0.25); // -> ball.x =  75
tween.advanceTime(0.25); // -> ball.x = 100
----

このやり方で確かに動きますが、これではちょっと面倒ですね。
`ENTER_FRAME` イベントハンドラ内で `advanceTime` を呼ぶこともできますが、
それでもやはり、アニメーションの数が増えてくると手間になってしまうでしょう。

心配しないでください。それをなんとかしてくれる "ヤツ" を知っています。
//原文：Don't worry: I know just the guy for you.
そのような処理の取り扱いに慣れているヤツです。
//原文：He's really good at handling such things.

=== Juggler

ジャグラー (Juggler）というクラスは、アニメーションするオブジェクトをいくつでも登録でき、アニメーションを再生してくれます。
実際の曲芸師(ジャグラー)のように、情熱をひたすらに遂行します。つまり、登録されたオブジェクトの `advanceTime` メソッドを呼び出し続けてくれます。
//原文：Like any true artist, it will tenaciously pursue its true passion, which is: continuously calling `advanceTime` on everything you throw at it.
Starling インスタンスには、デフォルトで利用できるジャグラーが１つ用意されています。
アニメーションを実行する最も簡単な方法は、下記の１行です。アニメーション（トゥイーン) をデフォルトのジャグラーに登録するだけで終わりです。

[source, as3]
----
Starling.juggler.add(tween);
----

トゥイーンが終了すると、すぐにアニメーションの登録が自動的に破棄されます。
たいていの場合、このシンプルなやり方で十分でしょう。

しかし別のケースでは、もっと細かく挙動をコントロールしたい事もあります。
例えば、ステージ上にメイン処理を行うゲームエリアがあるとします。
ユーザーがポーズボタンを押した時には、ゲームを停止させて、メッセージボックスをアニメーションとともに表示させ、
同時にゲームに戻るメニューもユーザーに提供するかもしれません。
//原文：maybe providing an option to return to the menu.

この場合、ゲーム部分は完全に停止するべきです。内部のアニメーションは進行させません。
しかし問題は、メッセージボックスもアニメーションを行うので、デフォルトのジャグラーを停止する事は出来ないという事です。

そのようなケースでは、ゲームエリアに固有のジャグラーを提供するのが良いでしょう。
ゲーム停止ボタンを押されるとすぐに、このジャグラーのアニメーション全てを停止しましょう。
ゲームは現在の状態のまま停止しますが、メッセージボックスのアニメーション（デフォルトのジャグラーか、また別のジャグラーを使っている）は普通に動きます。

カスタムジャグラーを使う場合、その `advanceTime` メソッドを毎フレーム呼んでやります。
下記のようにジャグラー制御するのが良いでしょう。

[source, as3]
----
public class Game // <1>
{
    private var _gameArea:GameArea;

    private function onEnterFrame(event:Event, passedTime:Number):void
    {
        if (activeMsgBox)
            trace("waiting for user input");
        else
            _gameArea.advanceTime(passedTime); // <2>
    }
}

public class GameArea
{
    private var _juggler:Juggler; // <3>

    public function advanceTime(passedTime:Number):void
    {
        _juggler.advanceTime(passedTime); // <4>
    }
}
----
<1> ゲームのルートクラスで、`Event.ENTER_FRAME` イベントをリッスンします。
<2> メッセージボックスが存在する際は、`gameArea` のみが進行するようにします。
<3> _GameArea_ は自身に専用のジャグラーを持っています。ゲーム中のアニメーション全てをそれが制御します。
<4> ジャグラーは _Game_ により呼ばれた `advanceTime` メソッド内で、進行させられます。

このやり方で、適切にゲームとメッセージボックスのアニメーションを分けることができます。

しかし、ジャグラーが扱うことができるのは、トゥイーンだけではありません。
`IAnimatable` インターフェースを実装したクラスであれば、なんでもジャグラーに登録できるのです。
このインターフェースには１つのメソッドしか定義されていません。

[source, as3]
----
function advanceTime(time:Number):void;
----

このメソッドを実装する事で、例えば簡単なムービークリップのようなクラスを自身で作る事もできます。
`advanceTime` メソッド内で、定期的に表示されているテクスチャーを切り替えればいいのです。
ムービークリップを再生するには、ただジャグラーに登録するでけで良いです。

さて、ここで疑問が残っているかもしれません。いつどのようにしてジャグラーから登録されたオブジェクトが取り除かれるのだろう？、という。

==== アニメーションの停止

トゥイーンが終了した際、ジャグラーから自動的に登録が取り除かれます。
もしもアニメーションを中断したい場合は、ジャグラーからトゥイーンをシンプルに取り除いてください。

ボールをアニメーションさせるトゥイーンを作成して、ジャグラーに登録したとします。

[source, as3]
----
tween:Tween = new Tween(ball, 1.5);
tween.moveTo(x, y);
Starling.juggler.add(tween);
----

このアニメーションを中断させる方法はいくつか存在します。
状況に応じて、一番適した方法を選んでください。

[source, as3]
----
var animID:uint = juggler.add(tween);

Starling.juggler.remove(tween); // <1>
Starling.juggler.removeTweens(ball); // <2>
Starling.juggler.removeByID(animID); // <3>
Starling.juggler.purge(); // <4>
----
<1> トゥイーンを直接削除します。この方法は、どんな `IAnimatable` に対しても有効です。
<2> ボールに影響するすべてのトゥイーンを削除します。これはトゥイーンだけに有効です。
<3> ID指定でトゥイーンを削除します。_Tween_ インスタンスへアクセスできないような場合に有効です。
<4> すべてを停止したい場合は、ジャグラーを purge します。

`purge` メソッドに関しては若干の注意をはらってください。
デフォルトのジャグラーに対してこのメソッドを実行した場合、別の場所のアニメーションが停止してしまい、ゲーム自体が停止してしまうかもしれません。
`purge` メソッドを使うのは、カスタムジャグラーだけにする事を推奨します。

==== アニメーション登録の自動削除

アニメーションが終了した際に、どのようにして _Tween_ クラスが、ジャグラーからトゥイーンを削除するのか、疑問に思ったかもしれません。
//原文:You might have asked yourself how the _Tween_ class manages to have tweens removed from the juggler automatically once they are completed.
これは、`REMOVE_FROM_JUGGLER` イベントを受け取ったタイミングで行われています。

`IAnimatable` を実装したオブジェクトは、このイベントを投げる事ができます。
ジャグラーはそのイベントを受け取り、それに応じてそのオブジェクトを登録から削除します。

[source, as3]
----
public class MyAnimation extends EventDispatcher implements IAnimatable
{
    public function stop():void
    {
        dispatchEventWith(Event.REMOVE_FROM_JUGGLER);
    }
}
----

==== 複数 Tween を１コマンドで実行する

ジャグラーとトゥイーンが分離されて扱える事はとてもパワフルですが、簡単なアニメーションの登録にたくさんのコードを記述しなくてはいけない状況もあるでしょう。
そこで、ジャグラー側には１コマンドでトゥイーンを実行できる便利なメソッドが用意されています。
下記がサンプルです。

[source, as3]
----
juggler.tween(msgBox, 0.5, {
   transition: Transitions.EASE_IN,
   onComplete: function():void { button.enabled = true; },
   x: 300,
   rotation: deg2rad(90)
});
----

このコードでは、`msgBox` オブジェクトにトゥイーンを設定しています。0.5秒間で `x座標` 及び `rotation` を変更するものです。
トゥイーン自体のプロパティと同じように、`{}` でくくっった引数で、オブジェクトにアニメーションさせたい項目が並んでいる事がわかるかと思います。
時間を節約する事ができますね。

=== 遅延実行

ここまでで、技術的な面では Starling がサポートするすべてのアニメーションをカバーし終えました。
しかし、このトピックに深く関係する、また別のコンセプトの機能があります。

_Einstein_ (犬のキャラです) が、イベントシステムを紹介した事を覚えていますか？
彼を最後に見た際は、ライフゲージが０であり、ゲームオーバーがコールされる直前でした。
しかし待ってください。すぐにゲームオーバーメソッドを実行しないで欲しいのです。それでは唐突にゲームが終わってしまいます。
代わりに、そうですね、２秒ほど後に実行してください。（それはプレイヤーが事態が変化した事を把握するのに十分な時間です。）
//原文：(time enough for the player to realize the drama that is unfolding).

この遅延処理を実装するには、ネイティブの _Timer_ か `setTimeout` メソッドを使うことができます。
しかし、ここでジャグラーを利用する事もできます。そしてジャグラーを使うやり方の方が、全てを制御する事ができるという大きな利点があります。

プレイヤーがその２秒が経たたないうちにポーズボタンを押すような可能性を考えると、その利点が明らかになるでしょう。
その際は、ゲームエリアのアニメーションを一時的に止めるだけでなく、この `gameOver` 呼び出しのタイミングも一時停止させておきたいはずです。

下記のように処理を実行しましょう。

[source, as3]
----
juggler.delayCall(gameOver, 2);
----

`gameOver` 関数は今から２秒後（もしジャグラーが一時停止されていればもっと後に）に実行されます。
このメソッドには引数を渡す事も可能です。代わりにイベントを実行したい場合は下記のようになります。

[source, as3]
----
juggler.delayCall(dispatchEventWith, 2, "gameOver");
----

delayed call を使うのとは別の便利な機能として、繰り返しの処理実行を行う、という機能があります。
３秒に１回敵キャラクターを生み出す事を考えてみてださい。

[source, as3]
----
juggler.repeatCall(spawnEnemy, 3);
----

[NOTE]
====
処理の裏側では、`delayCall` も `repeatCall` も _DelayedCall_ というタイプのオブジェクトを生成します。
`juggler.tween` メソッドが、トゥイーンを簡単に実行するためのメソッドだったのと同じように、それら２つのメソッドも delayed コール を作成するためのショートカットなのです。
====

遅延処理をキャンセルした場合は、下記のうちどちらかの命令を実行してください。

[source, as3]
----
var animID:uint = juggler.delayCall(gameOver, 2);

juggler.removeByID(animID);
juggler.removeDelayedCalls(gameOver);
----

=== ムービークリップ

_Mesh_ 周辺のクラス図を見た際に、すでに _MovieClip_ クラスの存在に気づいていたかもしれません。
//原文：You might have noticed the _MovieClip_ class already when we looked at the class diagram surrounding _Mesh_.
_MovieClip_ は _Image_ のサブクラスで、テクスチャーを時間経過に沿って変更するだけのクラスです。
//原文：That's right: a _MovieClip_ is actually just a subclass of _Image_ that changes its texture over time.
Starling 上でのアニメーションGIFだと理解しましょう。

==== テクスチャーの獲得
//原文：Acquiring Textures

ムービークリップのすべての画像は１枚のアトラスから読み込む事が推奨されます。それらの画像は、すべて同じ大きさである必要があります。
（もし同じサイズでない場合、１コマ目のサイズに変形されて表示されます。）
_Adobe Animate_ のようなツールを使って、そのようなアニメーションを用意する事ができます。Animate からは直接 Starling 用のテクスチャーアトラスデータを書き出す事ができます。

下記は、ムービークリップ用のフレームを持つテクスチャーアトラスデータのサンプルです。
まずはXML内に記述されている、フレーム座標の設定を確認しましょう。
どのフレームも `flight_` の名前で始まる事に注目してください。

[source, xml]
----
<TextureAtlas imagePath="atlas.png">
    <SubTexture name="flight_00" x="0"   y="0" width="50" height="50" />
    <SubTexture name="flight_01" x="50"  y="0" width="50" height="50" />
    <SubTexture name="flight_02" x="100" y="0" width="50" height="50" />
    <SubTexture name="flight_03" x="150" y="0" width="50" height="50" />
    <!-- ... -->
</TextureAtlas>
----

下記が対応するテクスチャです。

.ムービークリップの各フレーム。
image::flight-animation.png[Flight Animation]

==== ムービークリップの作成

さあ、ムービークリップを作成しましょう。
下記ソース中の atlas 変数が、すべてのフレームを含む _TextureAtlas_ の参照であるならば、話はとても簡単です。
//原文：Supposing that the `atlas` variable points to a _TextureAtlas_ containing all our frames, that's really easy.

[source, as3]
----
var frames:Vector.<Texture> = atlas.getTextures("flight_"); // <1>
var movie:MovieClip = new MovieClip(frames, 10); // <2>
addChild(movie);

movie.play();
movie.pause(); // <3>
movie.stop();

Starling.juggler.add(movie); // <4>
----
<1> `getTextures` メソッドは与えられた接頭語で始まるすべてのテクスチャーをABC順で返します。
<2> 設定はこれだけでOKです。テクスチャーの一覧をコンストラクタに渡します。
//原文：That's ideal for our _MovieClip_, because we can pass those textures right to its constructor. 
２番目のパラメータは１秒間に何回描画更新がかかるかを示します。
<3> ムービークリップの再生状態をコントロールするメソッドがいくつか存在します。デフォルトでは再生している状態から始まります。
<4> Important: 他の Starling 上のアニメーションと同じように、ムービークリップもジャグラーに登録される必要があります！

接頭語の `flight_` を使ってどのようにテクスチャアトラスからテクスチャーの一覧を得たか、わかったでしょうか？
このやり方であればアトラス画像には、ムービークリップとはまた別の画像を含ませる事も可能になります。
一つのムービークリップのフレームをグループ化する為に、それぞれで同じ接頭語を持たせるわけです。

ムービークリップには、サウンドや任意の処理を実行するため、あるフレームが再生された際に実行されるコールバックを仕込む事ができます。
APIリファレンスを確認して、何ができるのか把握しましょう！

==== さらに複雑なアニメーション

このアニメーションの仕組みの悪い面も伝えなくてはいけません。
//原文：A downside of this animation technique has to be mentioned, though:
もしもアニメーションの尺が長かったり、各フレームの画像が大きい場合、テクスチャーメモリーが足りなくなってしまう場合があるという事です。
アニメーションが複数の大きなテクスチャアトラスにまたがっている場合、メモリに収まらないでしょう。
//原文：If your animations take up several big texture atlases, they might not fit into memory.

そのようなタイプのアニメーションには、もっと手の込んだ作りの仕組みに切り替えましょう。それが、スケルタル（ボーン）アニメーションです。
そこでは、キャラクターはいくつかのパーツ（ボーン）に分解され、それぞれが別々にアニメーション制御されます。（そのキャラクターの骨格によります。）
これはとても柔軟な方法です。

そのようなアニメーション機能は Starling に含まれていません。しかし、いくつかの外部ツール・ライブラリが役立ちます。
下記のリストにあるソリューションはそれぞれ Starling とともに、とてもうまく動きます。

* http://esotericsoftware.com/[Spine]
* https://github.com/threerings/flump[Flump]
* http://dragonbones.github.io/[Dragon Bones]
* http://gafmedia.com[Generic Animation Format]
