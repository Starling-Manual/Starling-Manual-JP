== テクスチャとイメージ

すでにイメージとテクスチャのクラスについては何回か目にしているでしょう。そしてそれらは Starling の中で最も便利なクラスです。
しかしどのように使うのでしょうか。そして両者の違いはなんでしょう？

=== テクスチャ

テクスチャは画像を表すデータでしかありません。デジタルカメラに保存されたファイルのようなものです。
他の人にファイルそのものとして見せる事はないでしょう。それは、0と1のデータの集まりでしかないのですから。
イメージビューアで閲覧する、もしくは、プリンターへファイルを送る必要があります。

テクスチャは直接 GPUのメモリに格納されます。これは、レンダリング時にとても効率的にアクセスされるます。
テクスチャは swf に埋め込んだクラスからでも、動的にロードされたファイルからでも作製する事ができます。ファイルフォーマットは以下の中から選択可能です。

PNG:: もっとも多目的に使われるフォーマットです。劣化しない圧縮方式は、単色で広い領域に塗りがある画像に最適です。デフォルトのフォーマットとして推奨します。

JPG:: 非可逆圧縮であるため、写真、または写真のような画像では PNG よりも小さなファイルサイズとなります。しかし、 アルファチャンネルの情報を持たないため、使いにくい面もあります。大きな背景や写真を扱う場合におすすめです。

ATF:: 特に Stage3D 用として開発されたフォーマットです。メモリをあまり必要とせず、またロードも高速に行われます。しかし、見た目が劣化するため、全ての画像に向いているとは言えません。ATF テクスチャについては、この後のチャプターで取り扱います。( <<ATF Textures>> を参照。)

`starling.textures.Texture` クラスには、テクスチャをインスタンス化するためのたくさんのファクトリーメソッドが存在します。
いくつかを下記に羅列します。（それぞれ、引数は、わかりやすくするために省いています。）

[source, as3]
----
public class Texture
{
    static function fromColor():Texture;
    static function fromBitmap():Texture;
    static function fromBitmapData():Texture;
    static function fromEmbeddedAsset():Texture;
    static function fromCamera():Texture;
    static function fromNetStream():Texture;
    static function fromTexture():Texture;
}
----

おそらく最も一般的な処理はビットマップからテクスチャを作成する事でしょう。
とても簡単に行う事ができます。

[source, as3]
----
var bitmap:Bitmap = getBitmap();
var texture:Texture = Texture.fromBitmap(bitmap);
----

埋め込みビットマップからテクスチャを生成する事も同様に一般的です。
同じように対応できます。

[source, as3]
----
[Embed(source="mushroom.png")] // <1>
public static const Mushroom:Class;

var bitmap:Bitmap = new Mushroom(); // <2>
var texture:Texture = Texture.fromBitmap(bitmap); // <3>
----
<1> ビットマップの埋め込み。
<2> ビットマップをインスタンス化する。
<3> ビットマップからテクスチャを生成する。

しかし、もっと簡単に処理を行う方法があります。

[source, as3]
----
[Embed(source="mushroom.png")] // <1>
public static const Mushroom:Class;

var texture:Texture = Texture.fromEmbeddedAsset(Mushroom); // <2>
----
<1> ビットマップの埋め込み。
<2> 埋め込まれた画像のクラス定義からテクスチャを生成します。

[IMPORTANT]
.プロのテクニック
====
このように処理するとコード量が少ないだけでなく、メモリ消費量も少ないのです！

`fromEmbeddedAsset` メソッドは内部の処理がうまく働き、コンテキストのロスを防ぎます。また、`fromBitmap` メソッドが行うよりも、もっと効率的に働きます。
後ほどこのトピックについて触れますが、今のところは埋め込まれたビットマップからテクスチャを生成するにはこの方法が推奨される事を覚えておきましょう。
====

あまり目立たない _Texture_ クラスの別の機能に、 `fromTexture` というメソッドがあります。
//原文：Another feature of the _Texture_ class is hidden in the inconspicuous `fromTexture` method.
これは、テクスチャの一部の領域から別のテクスチャを生成するメソッドです。

このメソッドの素晴らしい点は、ピクセルのコピーが行われないという事実です。
代わりに、このメソッドによって作成された _SubTexture_ は、大元のテクスチャの参照を持ちます。
これは、とても効果的に働きます。

[source, as3]
----
var texture:Texture = getTexture();
var subTexture:Texture = Texture.fromTexture(
        texture, new Rectangle(10, 10, 41, 47));
----

この後すぐに _TextureAtlas_ クラスについて解説します。基本的に、TextureAtlas のために上記の仕組みは存在するのです。

=== Image

テクスチャについていくつか触れましたが、画面上に表示する方法をまだ知りません。
最も簡単な方法は Image クラスか、その仲間のクラスを利用する事です。

その仲間も合わせて詳しく見てみましょう。

[plantuml, mesh-classes]
....
include::../defaults.plantuml[]

Mesh <|-- Quad
Quad <|-- Image
Image <|-- MovieClip
....

* _Mesh_ は、三角ポリゴンの集合です。（GPU は三角形しか描画できません。）
* _Quad_ は、矩形を構成する少なくとも２つの三角ポリゴンの集合です。
* _Image_ は、Quad に便利なコンストラクタといくつかのメソッドを追加したものです。
* _MovieClip_ は、時間の経過に連れて内容を切り替える Image です。

これら全てのクラスはテクスチャを扱いますが、おそらく _Image_ クラスを最も頻繁に使う事になるでしょう。
なぜならば、矩形のテクスチャ表示は最も一般的で、それを扱うのに最も便利なのが Image クラスだからです。

_Quad_ と _Image_ を比較してみましょう。
//原文：To demonstrate, let me show you how to display a texture with a _Quad_ vs. an _Image_.

[source, as3]
----
var texture:Texture = Texture.fromBitmap(...);

var quad:Quad = new Quad(texture.width, texture.height); // <1>
quad.texture = texture;
addChild(quad);

var image:Image = new Image(texture); // <2>
addChild(image);
----
<1> quad を適切なサイズで生成し、テクスチャを紐づけます。または、
<2> image を標準的なコンストラクタで生成します。

個人的には、キーを打つ回数が少ない方をいつも選択します。
内部で処理される内容は２つのクラスで全く同じです。

.テクスチャが quad に適用された様子。
image::texture-mapping.png[Texture-Mapping]

=== １つのテクスチャを複数のImageに適用する

重要な事なのですが、テクスチャは複数の image (mesh) に適用する事ができます。
実際、そのように対応すべきです。テクスチャは１度だけロードして、アプリケーションが動いている間はそれを再利用しましょう。

[source, as3]
----
// このようにしてはいけません！
var image1:Image = new Image(Texture.fromEmbeddedAsset(Mushroom));
var image2:Image = new Image(Texture.fromEmbeddedAsset(Mushroom));
var image3:Image = new Image(Texture.fromEmbeddedAsset(Mushroom));

// 代わりに一度だけテクスチャを生成して参照を保持しましょう。
var texture:Texture = Texture.fromEmbeddedAsset(Mushroom));
var image1:Image = new Image(texture);
var image2:Image = new Image(texture);
var image3:Image = new Image(texture);
----

メモリ使用量のうち、テクスチャがほとんどの領域を占めるでしょう。テクスチャメモリを浪費するとすぐに全体のメモリも足りなくなってしまいます。

=== テクスチャアトラス

今までの全ての動作例では、テクスチャをそれぞれ個別にロードしました。
しかし、実際のアプリケーションでは、そのようにすべきではありません。
以下が理由です。

* GPU のレンダリングを効率的に行うため、Staring は、メッシュをバッチ処理でまとめて処理します。しかし、バッチ処理はテクスチャが切り替わると分断してしまいます。
* いくつかのシチュエーションでは、_Stage3D_ はテクスチャの縦横の大きさが２の倍数のサイズである事（power-of-two）を要求します。Staring は、この制限をうまく隠蔽しますが、サイズのルールに則っていない場合、余分なメモリを消費します。

テクスチャアトラスを使えば、テクスチャの切り替えと、power-of-two の制限を共に回避する事ができます。
全てのテクスチャを１つの大きなメインテクスチャに収め、Starling が、そこから適切な領域が描画されるようにうまく取り扱います。

.テクスチャアトラスの例。
image::penguflip-atlas.png[Texture Atlas]

このテクニックは、_Stage3D_ に小さなテクスチャではなく、大きなテクスチャを扱わせ、それぞれの quad にその大きなテクスチャの一部分を割り当てて描画させる方法です。
これにより、メモリ消費量を大幅に抑える事ができ、極力無駄がない状態とする事ができます。
（他のフレームワークはこの機能を _Sprite Sheets_ と呼びます。）

ifdef::backend-html5[]
TIP: "Texture Packer" の開発チームが、スプライトシートについて良い感じの紹介動画を作成しています。
ここで視聴可能です。 http://www.codeandweb.com/what-is-a-sprite-sheet[What is a Sprite Sheet?]
endif::[]

==== アトラスの作成

_SubTexture_ の位置は下記のような XMLファイル で定義されます。

[source, xml]
----
<TextureAtlas imagePath="atlas.png">
 <SubTexture name="moon" x="0" y="0" width="30" height="30"/>;
 <SubTexture name="jupiter" x="30" y="0" width="65" height="78"/>;
 ...
</TextureAtlas>;
----

このように、XML は一つの大きなテクスチャを参照し、複数の _SubTextures_ を設定します。それらはメインテクスチャの一部領域を指定します。
ランタイムでは、_SubTextures_ をその名前で参照する事が可能で、まるで１つの独立したテクスチャであるように扱う事ができます。

しかし、どのようにしてそれぞれのテクスチャをアトラスとしてまとめる事が可能なのでしょう？
幸い手作業で行う必要はありません。これを行うためのいくつものツールが存在します。
２つの候補を紹介しますが、Google で検索すればさらにたくさんの候補が見つかるでしょう。

* https://www.codeandweb.com/texturepacker/starling?source=gamua[TexturePacker] は、個人的に気に入っているツールです。これほどスプライトシートを調整する事ができるツールは他に見つからないでしょう。このツールは Starling のサポートもしており、とても素晴らしい物です。( ATF テクスチャを書き出せるツールが他にあるでしょうか？）
* http://renderhjs.net/shoebox/[Shoebox] _AIR_ で作られた無料のツールです。アトラスの生成について _TexturePacker_ ほどのたくさんの機能を持ってはいませんが、関連機能がたくさんあります。例えば、ビットマップフォントの作成機能や、スプライトの抽出機能があります。

==== アトラスの利用

テクスチャアトラスファイルを手に入れる事はできました。
では、どのようにそれを使えばいいでしょう？
では、テクスチャと XMLデータをソースコードに埋め込んでみましょう。

[source, as3]
----
[Embed(source="atlas.xml", mimeType="application/octet-stream")] // <1>
public static const AtlasXml:Class;

[Embed(source="atlas.png")] // <2>
public static const AtlasTexture:Class;
----
<1> アトラスの XML を埋め込みます。_mimeType_ の設定を忘れないようにしましょう。
<2> アトラスのテクスチャを埋め込みます。

NOTE: 代わりにそれぞれのファイルを URL から、または AIR としてアプリケーションが動いているのであればディスクからロードする事もできます。
この事については、_AssetManager_ の説明の際に詳しく取り扱います。

この２つのオブジェクトがあれば、_TextureAtlas_ インスタンスを生成する事ができ、`getTexture()` メソッドを通して全ての _SubTexture_ にアクセスする事ができます。
ゲームの初期化時に一度アトラスを生成しておけば、その後はいつでも参照して利用する事ができます。

[source, as3]
----
var texture:Texture = Texture.fromEmbeddedAsset(AtlasTexture); // <1>
var xml:XML = XML(new AtlasXml());
var atlas:TextureAtlas = new TextureAtlas(texture, xml);

var moonTexture:Texture = atlas.getTexture("moon"); // <2>
var moonImage:Image = new Image(moonTexture);
----
<1> アトラスを生成する。
<2> _SubTexture_　を表示する。

簡単ですよね！
//原文：It's as simple as that!

=== レンダーテクスチャー

_RenderTexture_ クラスを使うと、動的なテクスチャー生成が可能となります。
どんなディスプレイオブジェクトでも描画する事ができるキャンバスを思い浮かべてみてください。

レンダーテクスチャを作成した後に、`drawObject` メソッドを呼び出してテクスチャにオブジェクトを描画しましょう。
テクスチャ上にそのオブジェクトがその現在の位置で描かれ、回転・拡大・アルファも、そのまま適用されます。

[source, as3]
----
var renderTexture:RenderTexture = new RenderTexture(512, 512); // <1>

var brush:Sprite = getBrush(); // <2>
brush.x = 40;
brush.y = 120;
brush.rotation = 1.41;

renderTexture.draw(brush); // <3>
----
<1> 新規に _RenderTexture_ を、サイズ指定して作成します。（単位はポイントです。）初期値は完全に透明な状態です。
<2> このサンプルでは、筆で描画したような見た目のオブジェクトを描画ソースとして使います。適切な位置と回転角度を設定します。
<3> オブジェクトがテクスチャ上に現在の位置と回転角度で描画されます。

この描画処理はとても効率的に行われます。
テクスチャにオブジェクトを描画した後は、描画負荷的は通常のテクスチャと変わりません。レンダーテクスチャにどれだけのオブジェクトを描画したとしてもです。

[source, as3]
----
var image:Image = new Image(renderTexture);
addChild(image); // <1>
----
<1> レンダーテクスチャも通常のテクスチャと同じように扱えます。

たくさんのオブジェクトを一度に描画するのならば、`drawBundled` メソッドの処理ブロック内に描画処理をまとめる事を推奨します。
このようにすると、Starling がコストが高い内部処理をスキップする事ができ、かなりのスピードアップとなります。下記はその例です。

[source, as3]
----
renderTexture.drawBundled(function():void // <1>
{
    for (var i:int=0; i<numDrawings; ++i)
    {
        image.rotation = (2 * Math.PI / numDrawings) * i;
        renderTexture.draw(image); // <2>
    }
});
----
<1> 関数内に全ての描画命令をカプセル化して `bundled drawing` を有効にします。
<2> 関数内では、通常通り `draw` 命令を呼び出します。

レンダーテクスチャの一部を消去したい場合、ブレンドモードを BlendMode.ERASE に設定をする事で、ディスプレイオブジェクトを消しゴムのように使う事ができます。

[source, as3]
----
brush.blendMode = BlendMode.ERASE;
renderTexture.draw(brush);
----

完全に全てを消し去るには、`clear` メソッドを使って下さい。

[CAUTION]
.コンテキスト ロス
====
レンダーテクスチャには大きな欠点があります。それは、レンダーコンテキストが失われた際、レンダーテクスチャの内容が全てなくなってしまうという事です。
<<コンテキスト ロス>> は、この後のチャプターで詳しく取り扱います。一言で言えば、_Stage3D_ はいくつかのシチュエーションにおいて、全ての描画コンテンツの内容を失う事があるという事です。
（聞こえての通り、とても嫌な感じです。）
//原文：(Yes, that is as nasty as it sounds.)

従って、テクスチャの内容が重要であれば（例えば、それが単なる装飾目的の物でないのなら）、いくつかの努力を費やす必要があります。
このワークアラウンドについても後のチャプターで取り扱います。
//原文：We will look into possible strategies in the mentioned chapter
この事態に急に遭遇して驚ろいてしまわないように先にお知らせしておきました。
//原文： -- I just wanted to mention this fact here so it doesn't hit you by surprise.
====

// TODO: Mipmaps
// TODO: Image color
