== デバイスの回転

今日のスマートフォンとタブレットのとても素晴らしい機能に、デバイスの空間的な回転方向を認識して、
ユーザーインターフェースの向きを良い感じに調整してくれる、というものがあります。

Starling 上で回転方向の変化を認識するには、まず最初に AIR の設定ファイルを編集する必要があります。
下記のような設定としてください。

[source, xml]
----
<aspectRatio>any</aspectRatio> <1>
<autoOrients>true</autoOrients> <2>
----
<1> aspectRatio の設定を、`portrait`、`landscape`、`any`のいずれかにします。
<2> アプリの起動時に自動回転機能有効にするかどうかを指定します。

上記設定がうまくいけば、Starling の stage オブジェクトから `RESIZE` イベントを受け取ることができます。
このイベントは回転方向が変化した際に毎回発行されます。
結局のところ、回転方向の変化は必ず Stage サイズの変更も引き起こします。_幅_ と _高さ_ の入れ替わりです。

//原文：Update the dimensions of the Starling viewPort and stage in the corresponding event handler.
イベントを受け取った際には、Starling の描画エリアとStageの大きさを更新しましょう。

[source, as3]
----
stage.addEventListener(Event.RESIZE, onResize);

private function onResize(event:ResizeEvent):void // <1>
{
    updateViewPort(event.width, event.height);
    updatePositions(event.width, event.height);
}

private function updateViewPort(width:int, height:int):void // <2>
{
    var current:Starling = Starling.current;
    var scale:Number = current.contentScaleFactor;

    stage.stageWidth  = width  / scale;
    stage.stageHeight = height / scale;

    current.viewPort.width  = stage.stageWidth  * scale;
    current.viewPort.height = stage.stageHeight * scale;
}

private function updatePositions(width:int, height:int):void // <3>
{
    // Update the positions of the objects that make up your game.
}
----
<1> デバイスが回転した際、このイベントハンドラが呼び出されます。
<2> 現在のスクリーンサイズの大きさに合わせて、Stage と viewPort の大きさを調節します。
<3> 新しい回転の向きにマッチするよう、あなたのゲームのユーザーインターフェースを調整します。

イベントハンドラ内で、_viewPort_ と _stage の大きさ_ 両方を手動で更新しなくてはいけないことに注意してください。

デフォルトのままでは何の処理も行われません。つまり、アプリケーションの画面の一部がかけたような状態になってしまうでしょう。
上のコードはその問題を解決します。どんな scale factor 値でもうまく動作するでしょう。

//原文：The last part is going to be much harder: updating your user interface so that it fits into the new stage dimensions.
最後のステップは多少難しいかもしれません。あなたのアプリのインターフェースをうまく調整することです。
//原文：This does not make sense for all games -- but if it does, you should consider the additional effort.
全てのゲームでうまく調整できるわけではありませんが、うまくいく場合、ここに労力をかけるべきです。
//原文：Your users will appreciate it!
ユーザー視点で見れば喜ぶべき事です。

//原文：TIP: The _Scaffold_ project coming with Starling contains a possible implementation of this feature.
TIP: Starling を利用して作られたフレームワークの中にはすでにこの機能が実装されている場合もあるかと思います。