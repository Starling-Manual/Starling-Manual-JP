== カスタムスタイル

_Stage3D_ の生のパワーの領域へと足を踏み入れました。このまま突き進んでいきましょう。
このセクションでは、シンプルなメッシュスタイルのコードを記述します。
Starling 2 では、全てのレンダリング処理はスタイルを通じて行われます。
スタイルを自分自身で作る事で、パフォーマンスを犠牲にせずに独自な描画エフェクトを作ることができるのです。

NOTE: この先に進む前に、必ず <<カスタムフィルター>> のセクションも目を通しておいてください。
フィルターとスタイルでは多くのコンセプトを共通しています。ですので、より単純なフィルターから理解を始めた方が理にかなっています。
この先は、フィルターのセクションの内容は理解していると仮定して話を進めます。

=== ゴール

今回のゴールは _ColorOffsetFilter_ として作ったものと同じです。
描画ピクセルに対してカラーのオフセット指定ができるようにします。
これを今回はスタイルとして制作するわけです。これを _ColorOffsetStyle_ と呼びましょう。

.スタイルでカラーオフセットを指定する。
image::customfilter-offset.png[Offset with a Style]

まずは、フィルターとスタイルの違いについて理解するのが重要です。

==== フィルター vs スタイル

先に述べた通り、フィルターは各ピクセル１ドット１ドットに対して働きます。オブジェクトは一度テクスチャーに描画され、フィルターはそのテクスチャになんらかの処理します。
それに対してスタイルは、オブジェクトの元々の形状に対してアクセスします。もっと正確に言えば、オブジェクトの頂点に対して処理を行います。

スタイルにはある意味でそのような制限があるのですが（例えば、ブラー効果のスタイルを作ることはできません）、スタイルはフィルターよりもずっと効率的に処理を行います。
ます、フィルターでは必要だった一度テクスチャにオブジェクトを描画する最初のステップの処理が必要がありません。
さらに重要な事に、スタイルはバッチ処理の対象となる事ができます。

知っての通り、ドローコール数を低く抑えることは、高いフレームレートをキープするためにとても大事なことです。
そのためには、Starling にできる限りたくさんのオブジェクトをバッチ処置させる事が重要です。
ここで疑問となるのは、Starling がどのようにしてバッチ対象としてまとめるオブジェクトを決定しているかという事です。
ここで、スタイルが一役買っています。同じスタイルが設定され散るオブジェクトが同じバッチ処理の対象となる事ができます。

もしもステージ上に３つのイメージを配置して、それぞれに _ColorOffsetFilter_ を適用した場合、少なくとも３回のドローコールが確認できます。
代わりに _ColorOffsetStyle_ を適用した場合は、それがたったの１回となります。
このあたりに絡んだ処理がスタイルのコードを若干難しくしてしまってはいますが、同時にやり遂げる価値があるものとなっているのです！

=== MeshStyle を継承する

全てのスタイルのベースクラスとなるのは、`starling.styles.MeshStyle` です。
このクラスがスタイルの作成に必要な基盤機能を全て提供してくれます。まずは構造を確認してみましょう。

[source, as3]
----
public class ColorOffsetStyle extends MeshStyle
{
    public static const VERTEX_FORMAT:VertexDataFormat =
            MeshStyle.VERTEX_FORMAT.extend("offset:float4");

    private var _offsets:Vector.<Number>;

    public function ColorOffsetStyle(
        redOffset:Number=0, greenOffset:Number=0,
        blueOffset:Number=0, alphaOffset:Number=0):void
    {
        _offsets = new Vector.<Number>(4, true);
        setTo(redOffset, greenOffset, blueOffset, alphaOffset);
    }

    public function setTo(
        redOffset:Number=0, greenOffset:Number=0,
        blueOffset:Number=0, alphaOffset:Number=0):void
    {
        _offsets[0] = redOffset;
        _offsets[1] = greenOffset;
        _offsets[2] = blueOffset;
        _offsets[3] = alphaOffset;

        updateVertices();
    }

    override public function copyFrom(meshStyle:MeshStyle):void
    {
        // TODO
    }

    override public function createEffect():MeshEffect
    {
        return new ColorOffsetEffect();
    }

    override protected function onTargetAssigned(target:Mesh):void
    {
        updateVertices();
    }

    override public function get vertexFormat():VertexDataFormat
    {
        return VERTEX_FORMAT;
    }

    private function updateVertices():void
    {
        // TODO
    }

    public function get redOffset():Number { return _offsets[0]; }
    public function set redOffset(value:Number):void
    {
        _offsets[0] = value;
        updateVertices();
    }

    // 他のカラーオフセット用のプロパティも同じように実装される必要があります。

    public function get/set greenOffset():Number;
    public function get/set blueOffset():Number;
    public function get/set alphaOffset():Number;
}
----

これが、作業のとっかかりとなるコードです。
フィルター作成時のそれよりも若干先に進んだ状態となっているので、
//原文：You'll see that there's already going on a little more than in our initial filter class from the last example.
個々のメソッドを確認してみましょう。

==== 頂点フォーマット

頂点フォーマットが定数としてクラスの上部に記述されている事に注目してください。
スタイルは頂点レベルで働くと先に述べました。オブジェクトの全ての形状情報にアクセスが可能です。
//原文：I mentioned already that styles work on a vertex level, giving you access to all the geometry of an object.
_VertexData_ クラスはその形状を保持しています。しかし、各頂点にどんなデーターが保持されているか、どうやって保持させるか、についてはまだ述べていませんでした。
まさにそれが、_VertexDataFormat_ によって定義されているのです。

_MeshStyle_によって扱われるデフォルトのフォーマットは以下のようになっています。

  position:float2, texCoords:float2, color:bytes4

この文字列の書式には馴染みがあるように見えます。これは属性情報とデータ型の羅列です。

* `position` 属性は２つの float 型を保持します。 (頂点のX座標、Y座標です。）
* `texCoords` 属性も２つの float 型を保持します。 (テクスチャ座標を表します。).
* `color` 属性は４バイトの色情報を保持します。 (１バイトごとに１チャンネルの情報です。)

このフォーマットの _VertexData_ インスタンスは、メッシュの各頂点にそれら属性値を保持します。属性値の格納順はフォーマット文字列と同じ順序です。
つまり、各頂点は合計20バイト（8バイト + 8バイト + 4バイト）を保持する事になります。

もしもメッシュになんのスタイルも当てはめなかった場合、標準の _MeshStyle_ を用いて描画処理が行われます。
各頂点の属性情報には上記のフォーマットが適用されます。
それが、各頂点にテクスチャと色味を付与したメッシュを描画するために必要な情報です。

しかし、_ColorOffsetStyle_ にはこれで属性情報が十分ではありません。カラーオフセット値も保持させる必要があります。
さらに４つのfloat値からなる `offset` 属性を追加した新しいフォーマットを定義する必要があるのです。

[source, as3]
----
MeshStyle.VERTEX_FORMAT.extend("offset:float4");
// => position:float2, texCoords:float2, color:bytes4, offset:float4
----

さて、ここでまた疑問が湧くかもしれません。
"なぜこんな作業が必要なのか？フィルターでは頂点フォーマットを調整しなくてもうまくいったじゃないか" と。

これはとても良い質問です。（質問されて嬉しいです！）
答えは、Starlng のバッチ処理にあります。
１つのスタイルを次に描画するメッシュにも適用できる場合にそれらのメッシュがバッチ処理されます。これが上記のような労力を払う理由です。

しかし、バッチ処理とはそもそもなんでしょう？
それは、個々のメッシュの頂点データをまとめて１つの大きなメッシュとしてレンダリングする事を意味します。
Starling のレンダリングコードの中で、下記のようなコードを見つけることができるでしょう。

[source, as3]
----
var batch:Mesh = new Mesh();

batch.add(meshA);
batch.add(meshB);
batch.add(meshC);

batch.style = meshA.style; // ← !!!
batch.render();
----

ここで注目すべき箇所がわかるでしょうか？大きなメッシュ（＝`batch`）には最初のメッシュのスタイルのコピーが適用されています。
しかし、３つのメッシュのスタイルはそれぞれ異なった設定になっているかもしれません。

もしもその異なった設定が _style_ の中にだけ保持されているとしたら、最初の１つのメッシュをのぞき、その設定が失われてしまいます。
そうならないように、スタイルはデーターをターゲットとなるメッシュの頂点に格納しなくてはいけません。
そうすれば、巨大なメッシュは全てのオフセット情報を個別に受け取ることができます。

IMPORTANT: この事はとても重要なので、２回言います。
*スタイルの設定はいつでもターゲットとなるメッシュの頂点データに格納される必要があります。*

頂点フォーマットはいつでもスタイルクラスの静的な定数としてアクセスする事ができ、`vertexFormat` プロパティとしてもアクセスが可能です。
//原文：Per convention, the vertex format is always accessible as a static constant in the style's class, and also returned by the `vertexFormat` property.
スタイルがメッシュに適用される際、メッシュの頂点は自動的に新しいデータフォーマットが適用されます。

このコンセプトを理解したのなら、このチャプターの半分はなし遂げたことになります。
残りは、フラグメントシェーダー定数ではなく、頂点データからオフセット値を読みとるようにコードをアップデートするだけです。

その件は後に説明します。まずは先に進みましょう。
//原文：But I'm getting ahead of myself.

==== メンバ変数

全てのデーターは頂点に保持されると言いましたが、同時にクラスのメンバ変数にもデーターを保持させています。
//原文：You'll note that even though I just insisted that all data is to be stored in the vertices, there's still a set of offsets stored in a member variable:

[source, as3]
----
private var _offsets:Vector.<Number>;
----

なぜならば、実際にメッシュにスタイルが適用される前に、開発者がスタイルのプロパティを調整できるようにしたいからです。
ターゲットとなるオフジェクトがまだ紐づけられていない場合、頂点にデーターを保持させる事ができません。
それなので、一旦このベクター型のメンバ変数にデータを保持させます。
ターゲットとなるオブジェクトが紐づけられると、それらのデーターはターゲットの頂点へとコピーされます。
（`onTargetAssigned` メソッドを確認してみて下さい。）

==== copyFrom

バッチ処理の間、スタイルは時々（内部の値を）コピーされて使われる事があります。主な理由はスタイルを再利用し、苛だたしいガーベジコレクションを抑制するためです。
そのためには、`copyFrom` メソッドをオーバーライドする必要があります。
実際のコードは下記のようになります。

[source, as3]
----
override public function copyFrom(meshStyle:MeshStyle):void
{
    var colorOffsetStyle:ColorOffsetStyle = meshStyle as ColorOffsetStyle;
    if (colorOffsetStyle)
    {
        for (var i:int=0; i<4; ++i)
            _offsets[i] = colorOffsetStyle._offsets[i];
    }

    super.copyFrom(meshStyle);
}
----

単純なコードです。
もしもスタイルが正しい型である場合、現在のインスタンスのオフセット値をコピーしています。
続きの処理はスーパークラスが受け持ってくれます。

==== createEffect

このメソッドには見覚えがありますね？

[source, as3]
----
override public function createEffect():MeshEffect
{
    return new ColorOffsetEffect();
}
----

これはフィルターの createEffect と同じように働きます。後ほど作成する `ColorOffsetEffect` を戻り値とします。
ColorOffsetEffect はフィルタークラスで使ったものと同じではありません。（オフセット情報が頂点から読み取られています。）
しかし、フィルターとスタイルの両方で動くエフェクトを作る事は可能です。

==== onTargetAssigned

オフセット値をは、ターゲットとなるメッシュの頂点に保持する必要があります。
*全て* の頂点に *同じ値* のオフセット値を保持させます。これは一見無駄な処理に思えるかもしれません。
しかしこの方法がスタイルがバッチ処理をサポートすようにできる唯一の方法なのです。

スタイルがターゲットにアサインされるとこの onTargetAssigned コールバックが呼び出されます。頂点データを更新するきっかけのタイミングです。
//原文：フィルターと書いてあるが、間違いだろう。When the filter is assigned a target, this callback will be executed -- that is our cue to update the vertices.
また後にもこの処理を行うことになるので、実際の処理は `updateVertices` メソッドに移動しました。

[source, as3]
----
override protected function onTargetAssigned(target:Mesh):void
{
    updateVertices();
}

private function updateVertices():void
{
    if (target)
    {
        var numVertices:int = vertexData.numVertices;
        for (var i:int=0; i<numVertices; ++i)
            vertexData.setPoint4D(i, "offset",
                _offsets[0], _offsets[1], _offsets[2], _offsets[3]);

        setRequiresRedraw();
    }
}
----

`vertexData` 変数はどこからやってきたのだろう？と疑問に思うかもしれません。
ターゲットのオブジェクトがアサインされるとすぐに `vertexData` はターゲットの頂点を参照するようになります。
（スタイル自身は頂点データを保持していません。）
よって、このコードではターゲットの全ての頂点をループで回し、正しいオフセット値を設定し、レンダリングのための準備を行っています。

=== MeshEffect を継承する

スタイルクラスの設定は終わりました。次は実際のレンダリングが行われているエフェクト処理へ移りましょう。
今度は _MeshEffect_ クラスを継承します。
エフェクトではローレベルなレンダリングコードを書くだけである事を覚えていますか？
解説は、下記のようなクラスの継承関係を踏まえて進めます。
//原文：I'm actually talking about a group of classes with the following inheritance:

[plantuml, effect-classes]
....
include::../defaults.plantuml[]

Effect <|-- FilterEffect
FilterEffect <|-- MeshEffect
....

ベースクラス（ _Effect_ ）は、とても小さいクラスです。三角ポリゴンを描画するだけです。
_FilterEffect_ は、それにテクスチャのサポートを加え、_MeshEffect_ はwqらにそこにカラーとアルファの扱いを加えます。

NOTE: これら２つのクラスは _TexturedEffect_ と _ColoredTexturedEffect_ という名前にする事もできました。しかし、実際にそのクラスが利用される場所を考慮に入れて別の名前としました。
フィルターを作るのであれば、_FilterEffect_ を継承する必要があります。メッシュスタイルを作るのであれば、_MeshEffect_ を継承します。

_ColorOffsetEffect_ の構成をざっと見てみましょう。実際の実装はこの後行います。
//原文：So let's look at the setup of our _ColorOffsetEffect_, with a few stubs we're filling in later.

[source, as3]
----
class ColorOffsetEffect extends MeshEffect
{
    public  static const VERTEX_FORMAT:VertexDataFormat =
        ColorOffsetStyle.VERTEX_FORMAT;

    public function ColorOffsetEffect()
    { }

    override protected function createProgram():Program
    {
        // TODO
    }

    override public function get vertexFormat():VertexDataFormat
    {
        return VERTEX_FORMAT;
    }

    override protected function beforeDraw(context:Context3D):void
    {
        super.beforeDraw(context);
        vertexFormat.setVertexBufferAt(3, vertexBuffer, "offset");
    }

    override protected function afterDraw(context:Context3D):void
    {
        context.setVertexBufferAt(3, null);
        super.afterDraw(context);
    }
}
----

フィルターの場合と比較すると、全てのオフセット値設定が削除されているのがわかるでしょう。
その代わりに `vertexFormat` メソッドをオーバーライドしています。オフセット値は頂点データに格納された状態で受け取るため、スタイルに対応した頂点フォーマットを使っています。

==== beforeDraw と afterDraw

`beforeDraw` と `afterDraw` メソッドは、シェーダーが `va3` レジスタ (_vertex attribute 3_) から値を読み込めるようにコンテキストを設定します。
では、`beforeDraw` をみてみましょう。

[source, as3]
----
vertexFormat.setVertexBufferAt(3, vertexBuffer, "offset");
----

上記のコードは下記と同等です。

[source, as3]
----
context.setVertexBufferAt(3, vertexBuffer, 5, "float4");
----

三番目の引数 (`5 → bufferOffset`) は、頂点データのうち何番目がカラーオフセット値を示しているのかを表しています。
最後の引数 (`float4 → format`) は、属性値のフォーマットを示します、
よって、これらの値について覚えておいたり再計算したりする必要はありません。`vertexFormat` にそれらの属性を設定するように頼めばいいのです。
この方法なら、もし頂点フォーマットが変更された場合でも、さらに他の属性値がオフセット値の前に追加された場合でも、コードがそのまま動作するようにする事ができます。
//原文：That way, the code will continue to work if the format changes and we add, say, another attribute before the offset.

頂点の属性値は描画が終了した後に必ずクリアーされていなければいけません。なぜなら後続のドローコールが別のフォーマットで利用する可能性があるからです。
それが `afterDraw` メソッドで行う処理となります。

==== createProgram

ついにスタイルのコードの核心に迫ります。AGAL コードで実際にレンダリング処理を行う部分です。
バーテックスシェーダをにも改変を入れていきます。今回は標準の実装のままではうまくいかないのです。
//原文：This time, we have to implement the vertex-shader as well; it won't do to use a standard implementation, because we need to add some custom logic.
一方、フラグメントシェーダーのものとほぼ同じとなります。
確認してみましょう。

[source, as3]
----
override protected function createProgram():Program
{
    var vertexShader:String = [
        "m44 op, va0, vc0", // 4x4行列で頂点座標をクリップスペースに変換します
        "mov v0, va1     ", // テクスチャ座標をフラグメントシェーダーに送ります
        "mul v1, va2, vc4", // アルファ値(vc4)とカラー値(va2)を掛け合わせてフラグメントシェーダーに送ります
        "mov v2, va3     "  // カラーオフセット値をフラグメントシェーダーに送ります
    ].join("\n");

    var fragmentShader:String = [
        tex("ft0", "v0", 0, texture) +  // テクスチャからカラーを読み取ります
        "mul ft0, ft0, v1",             // カラー値とテクスチャカラーを掛け合わせます
        "mov ft1, v2",                  // オフセット値をft1レジスタにコピーします
        "mul ft1.xyz, v2.xyz, ft0.www", // オフセットのrgb値をアルファ値を掛け合わせます(pma対応)
        "add oc, ft0, ft1"              // カラーオフセット値とカラー値を足し合わせて最終出力とします
    ].join("\n");

    return Program.fromSource(vertexShader, fragmentShader);
}
----

バーテックスシェーダーが何をしているのか理解するには、まず始めに入力情報について理解しなくてはいけません。
//原文：To understand what the vertex-shader is doing, you first have to understand the input it's working with.

* `va` レジスタ ("vertex attribute" レジスタ) は、バーテックスバッファーから得られた頂点の属性値情報を持ちます。属性値は先ほどバーテックスフォーマットで設定した順序で格納されています。それぞれ、`va0` は座標、`va1` はテクスチャ座標、`va2` はカラー、`va3`はカラーオフセットの情報です。
* ２つの定数は全ての頂点で共通となります。`vc0-3` はmvpマトリックス、`vc4` は現在のアルファ値です。

どのバーテックスシェーダーでも行う主な処理とは、頂点座標をクリップスペースと呼ばれる座標空間に変換する事です。
これは、頂点座標と`mvpマトリックス` (modelview-projection matrix) と掛け合わせる事で行う事ができます。
最初の行でその処理が行われます。これは Starling のどのバーテックスシェーダーでも見かける処理です。
頂点が画面上でどこに位置するのか求めている、という説明で十分でしょう。

それとは別に、"varying レジスタ" の `v0` から `v2` を通じて、いくらかのデータをフラグメントシェーダーに送っています。

フラグメントシェーダーの内容はフィルタークラスのものとほぼ同じような実装となっています。
違いがわかるでしょうか？
それは、カラーオフセット値を読み取っているレジスタです。フィルターでは定数レジスタ、今回のスタイルでは `v2` レジスタにそれが保持されています。

=== 動作テスト

オリジナルのスタイルの作成の完了まであと一歩です！
//原文：There you have it: we're almost finished with our style!
動作テストを行ってみましょう。
いきなりですが、２つのオブジェクトにスタイルを適用してみます。バッチ処理が正しく動いている事が確認できるはずです。
//原文：In a truly bold move, I'll use it on two objects right away, so that we'll see if batching works correctly.

[source, as3]
----
var image:Image = new Image(texture);
var style:ColorOffsetStyle = new ColorOffsetStyle();
style.redOffset = 0.5;
image.style = style;
addChild(image);

var image2:Image = new Image(texture);
image2.x = image.width;
var style2:ColorOffsetStyle = new ColorOffsetStyle();
style2.blueOffset = 0.5;
image2.style = style2;
addChild(image2);
----

.２つのスタイルのイメージがドローコール１回で描画されています。
image::custom-style-sample.png[Custom Style Sample]

やりました、正しく動いています！
ここで、左上のドローコール表示が１のままである事を確認してください。

…まだ少しやらなけらばならない事があります。今回のシェーダーコードはテクスチャからデータを読み出す前提になっていますが、
スタイルはテクスチャを使わないメッシュに適用される事もあり得るので、そのようなケースのために特別なコードを書かなくてはいけないでしょう。
（今ここで解説はしませんが、とてもシンプルなものです。）

その点を修正した完全なコードは ここ: https://gist.github.com/PrimaryFeather/eaaa4a1b50cf546613d2[ColorOffsetStyle.as] で確認が可能です。

=== この先にたどり着く場所
//原文：Where to go from here

スタイルについては以上です！
私と同じように、スタイルの作成を楽しんでくれたなら良いのですが。
上記のやり方は、あなたの頭の中にある Starling 拡張を実現するキーとなるものです。
//翻訳苦しい 原文：What you see above is the key to extending Starling in ways that are limited only by your imagination.
_MeshStyle_ クラスにはまだいくつか実装ポイントがあります。クラスドキュメントも通して確認するようにしてください。
//原文：The _MeshStyle_ class even has a few more tricks up its sleeve, so be sure to read through the complete class documentation.

みんなが作ったスタイルを見る事を楽しみにしています！
