== カスタムフィルター

手を動かす準備はできていますか？
ついにカスタムレンダリングのコードに取り掛かろうとしています。まずは簡単なフラグメントフィルターからです。

フィルターの作成には低レイヤーのコードを扱う必要があります。何行かのアセンブラコードを書く事にもなるでしょう。
しかし恐れる事はありません。ロケット工学を扱うわけではないのですから。
昔の数学の先生は言ったものです。訓練を積んだ猿でもそんな問題は解けてしまうぞ！と。
//原文：As my old math teacher used to say: a drilled monkey could do that!

NOTE: 覚えておいて欲しいのですが、フィルターはディスプレイオブジェクトのピクセル単位で動きます。
フィルターが適用されたディスプレイオブジェクトは、テクスチャーに変換され、その後フラグメントシェダーで処理されます。（よってこれをフラグメントフィルターを呼びます。）

=== ゴール

簡単なフィルターを作ってみる事をこのチャプターのゴールとしますが、実際に役に立つものを作りたいですね。
ここでは _ColorOffsetFilter_ を作る事にしましょう。

メッシュ各頂点にはカラー値が設定でき、メッシュに色付けをする事ができる事はおそらく知っているかと思います。
そのカラー値は、レンダリング時にテクスチャーのカラー値と乗算されます。テクスチャーの色味を調整するシンプルで高速なやり方です。

[source, as3]
----
var image:Image = new Image(texture);
image.color = 0x808080; // R = G = B = 0.5
----

背景の数学的な演算はとてもシンプルです。GPU では赤・緑・青のどのチャンネルも 0 から 1 の間の数値で表されます。
例えば、純粋な赤は下記のような値になります。

  R = 1, G = 0, B = 0

レンダリング時に、このカラー値は各ピクセルのテクスチャー色（テクセルとも呼ばれます）と乗算されます。
デフォルトのイメージのカラーは純粋な白です。全てのチャンネル値が１となります。
したがって、テクセルのカラーは変化しません。（１を掛ける事は何もしない事と同じです。）

異なったカラー値を設定すると乗算結果は新しい色となります。下記はその例です。

  R = 1,   G = 0.8, B = 0.6  ×
  R = 0.5, G = 0.5, B = 0.5
  -------------------------
  R = 0.5, G = 0.4, B = 0.3

さて、ここで問題が起きます。この処理ではオブジェクトの色を暗くはできますが、絶対に明るくはできないのです。
何故ならば掛け合わせている値が 0 と 1 の間だけだからです。0 をかける事は黒を作り出します。そして 1 をかける変化がありません。

.グレーのカラー設定で色味が変化した画像。
image::customfilter-tinting.png[Tinting]

この問題をこれから作るフィルターで解決してみましょう。
カラーの計算にオフセット値を含むようにしてみます。
（Flashでは http://help.adobe.com/en_US/FlashPlatform/reference/actionscript/3/flash/geom/ColorTransform.html[ColorTransform] の設定として行なっていた事と同じです。)

* 変化後の red 値 = (元の red 値 × red 乗算値) + red オフセット値
* 変化後の green 値 = (元の green 値 × green 乗算値) + green オフセット値
* 変化後の blue 値 = (元の blue 値 × blue 乗算値) + blue オフセット値
* 変化後の alpha 値 = (元の alpha 値 × alpha 乗算値) + alpha オフセット値

乗算する部分の機能はメッシュの基底クラスがすでに持っているので、オフセット値のみがこれから作るフィルターに必要なプロパティとなります。

.全てのチャンネルにオフセット値を与える。
image::customfilter-offset.png[Offset]

では、そろそろ始めますよ？

=== FragmentFilter を継承する

あらゆるフィルターは例外なく、`starling.filters.FragmentFilter` クラスを継承して作ります。

ここで、 _ColorOffsetFilter_ クラスの完全なコードを紹介します。仮コードではなく最終のコードです。
この後のコード修正はありません。

[source, as3]
----
public class ColorOffsetFilter extends FragmentFilter
{
    public function ColorOffsetFilter(
        redOffset:Number=0, greenOffset:Number=0,
        blueOffset:Number=0, alphaOffset:Number=0):void
    {
        colorOffsetEffect.redOffset = redOffset;
        colorOffsetEffect.greenOffset = greenOffset;
        colorOffsetEffect.blueOffset = blueOffset;
        colorOffsetEffect.alphaOffset = alphaOffset;
    }

    override protected function createEffect():FilterEffect
    {
        return new ColorOffsetEffect();
    }

    private function get colorOffsetEffect():ColorOffsetEffect
    {
        return effect as ColorOffsetEffect;
    }

    public function get redOffset():Number
    {
        return colorOffsetEffect.redOffset;
    }

    public function set redOffset(value:Number):void
    {
        colorOffsetEffect.redOffset = value;
        setRequiresRedraw();
    }

    // その他の オフセット プロパティも同じようにそれぞれ実装します。

    public function get/set greenOffset():Number;
    public function get/set blueOffset():Number;
    public function get/set alphaOffset():Number;
}
----

驚くほどコンパクトですね。
実は、これはまだこのチャプターの話題の半分ほどです。もうひとつ別のクラスを記述する必要があります。そちらで、実際の色味を演算する処理が入ります。
しかし、上記のコードも実装の詳細を確認する価値があります。

このクラスは _FragmentFilter_ クラスを継承しており、'createEffect' メソッドをオーバーライドしています。
おそらく `starling.rendering.Effect` クラスを扱った事はない人がほとんどだと思いますが、それはローレベルのレンダリング用途でしか必要とならないからです。
API ドキュメントからの引用ですが、下記のような説明がされます。

====
Effect クラスは Stage3D の描画工程の全てをカプセル化します。
レンダーコンテキストを設定し、インデックスバッファ、バーテックスバッファ、シェーダプログラムをセットアップします。
全ての基本的でローレベルなレンダリングメカニズムを提供してくれます。
//原文：It configures the render context and sets up shader programs as well as index- and vertex-buffers, thus providing the basic mechanisms of all low-level rendering.
====

_FragmentFilter_ クラスは、このクラスもしくはこのクラスを継承した _FilterEffect_ というサブクラスを利用します。
今回のシンプルなフィルターの制作のためには、`createEffect()` をオーバライドし、カスタムエフェクトが戻り値となるように実装してやらなくてはいけません。
//原文：For this simple filter, we just have to provide a custom effect, which we're doing by overriding `createEffect()`.
ソース中のその他のプロパティは、エフェクトを調整するためだけのものです。
実際のレンダリング時には、ベースクラスが、effect インスタンスを扱い、フィルターの描画処理を行なってくれます。
取り急ぎ、以上となります。

NOTE: `colorOffsetEffect` プロパティが何をしているのか気になるでしょうか。
//原文：If you're wondering what the `colorOffsetEffect` property does:
これは、 _ColorOffsetEffect_ へのキャストを行わずに effect へのアクセスを行うためのただのショートカットです。
ベースクラスは `effect` プロパティを提供します。しかし、それは FilterEffect` 型でのオブジェクトであり、
`offset` プロパティにアクセスするためには、完全な _ColorOffsetEffect_ 型が必要であるわけです。

もっと複雑なフィルターでは `process` メソッドの継承も必要になるかもしれません。例えばマルチパスのフィルターを作る場合などです。
しかし、今回のサンプルフィルターでは必要ありません。

最後に、`setRequiresRedraw` メソッドの呼び出しについて記します。
このメソッドを呼び出す事で、フィルターの設定値が変化した際に必ずエフェクトの再描画がかかるようにしています。
この呼び出しがないと、Starling はオブジェクトを再描画するべきタイミングを知る事ができないのです。

=== FilterEffect を継承する

実作業を行う時がやってきました。
この _FilterEffect_ のサブクラスは、やる事が多く、サンプルフィルター内の働き者クラスと言えます。
それは、複雑である、という意味ではありません。しばらく我慢しておつきあいください。
//原文：Which doesn't mean that it's very complicated, so just bear with me.

先ずは仮のスタブコードから始めましょう。

[source, as3]
----
public class ColorOffsetEffect extends FilterEffect
{
    private var _offsets:Vector.<Number>;

    public function ColorOffsetEffect()
    {
        _offsets = new Vector.<Number>(4, true);
    }

    override protected function createProgram():Program
    {
        // TODO
    }

    override protected function beforeDraw(context:Context3D):void
    {
        // TODO
    }

    public function get redOffset():Number { return _offsets[0]; }
    public function set redOffset(value:Number):void { _offsets[0] = value; }

    public function get greenOffset():Number { return _offsets[1]; }
    public function set greenOffset(value:Number):void { _offsets[1] = value; }

    public function get blueOffset():Number { return _offsets[2]; }
    public function set blueOffset(value:Number):void { _offsets[2] = value; }

    public function get alphaOffset():Number { return _offsets[3]; }
    public function set alphaOffset(value:Number):void { _offsets[3] = value; }
}
----

ここでは、offsets プロパティを _Vector_ 型の中に格納しています。その方が GPU へのアップロード時に都合がいいからです。
`offset` プロパティを、ベクターの中に読み書きする。シンプルです。

２つの継承メソッドについて見ていくと、さらに面白くなってきます。

==== createProgram

createProgram メソッドは、実際の Stage3D シェーダーコードを作成します。

[NOTE]
====
基本的な部分は解説しますが、_Stage3D_ の全てについて対象とするのはこのマニュアルの範疇を超えます。
詳細な情報が必要であれば、下記リンクのチュートリアルを参照してください。

  * http://www.adobe.com/devnet/flashplayer/articles/how-stage3d-works.html[How Stage3D works]
  * http://jacksondunstan.com/articles/1661[Introduction to AGAL]
  * http://help.adobe.com/en_US/as3/dev/WSd6a006f2eb1dc31e-310b95831324724ec56-8000.html[List of AGAL operations]
====

Stage3D のレンダリングは、全てバーテックス()シェーダーとフラグメント(断片)シェーダーを通して行われます。
それらは GPU によって直接実行される小さなプログラムで、２つの役割を持ちます。

* *バーテックスシェーダー* は各頂点に対してそれぞれ１度だけ実行されます。
入力情報は、頂点の属性値から作成され、通常 `VertexData` クラスを通して設定されます。
出力情報は、スクリーン座標系での位置情報です。
* *フラグメントシェーダー* は１ピクセル（=断片=フラグメント）ごとに１回実行されます。
入力情報は関連する３つの頂点の情報がピクセルの位置によって補完されたものです。
出力情報は、シンプルにそのピクセルのカラー情報です。
* ２つのシェーダが合わさって１つの *Program* を構成します。

フィルターを記述する言語は AGAL と呼ばれます。アセンブリ言語です。
（最初に述べた通り、この言語はローレベル中のローレベルです。）
//原文：(Yes, you read right! This is as low-level as it gets.)
幸いな事に、大抵の AGAL プログラムはとても短く、そこまで大変ではありません。
//原文：Thankfully, however, typical AGAL programs are very short, so it's not as bad as it sounds.

そして、良いニュースとして、今回はフラグメントシェーダーを書くだけですみます。
バーテックスシェーダーは大抵のフラグメントフィルターで同じになるので、Starling は標準的なバーテックスシェーダーの実装をあらかじめ用意しています。
では、そのコードを見てみましょう。

[source, as3]
----
override protected function createProgram():Program
{
    var vertexShader:String = STD_VERTEX_SHADER;
    var fragmentShader:String =
        "tex ft0, v0, fs0 <2d, linear> \n" +
        "add oc, ft0, fc0";

    return Program.fromSource(vertexShader, fragmentShader);
}
----

バーテックスシェーダーは定数（ STD_VERTEX_SHADER ）から取得されています。
フラグメントシェーダーはたったの２行です。
２つのシェーダーは合わせて１つの _Program_ インスタンスとなり、createProgram メソッドの戻り値となっています。

フラグメントシェーダー部分は引き続き詳細を追ってみましょう。
//原文：The fragment shader requires some further elaboration, of course.

===== AGAL コード概要

AGAL コードは、各行にシンプルな命令が記述されています。

  [オペコード] [出力場所], [引数１], ([引数２])

* 最初の３文字はオペーレーション名＝命令の名前です。 (`tex`, `add`).
* 次の引数はオペレーション結果をどこに保存するかを指定します。
* 他の引数はオペレーションへの実際の引数となるものです。
* 全てのデータは "レジスタ" に保持されます。
レジスタは _Vector3D_ インスタンスだと考えてみましょう。x、y、zのプロパティを持っています。

レジスタにはいくつかの種類があります。定数、一時データ、シェーダーの出力用などです。
今回のシェーダーでは、そのうちのいくつかはあらかじめ値を持っています。フラグメントフィルターの as コードによって設定されています。
（後ほど解説します。）

* `v0` (_varying レジスタ 0_)は、現在のテクスチャ座標を保持しています。
* `fs0` (_fragment サンプラ 0_)は、入力テクスチャを示します。
* `fc0` (_fragment 定数 0_)は、カラーオフセット値を保持しています。今回のフィルターのメインとなる部分です。
//原文：contains the color offset this is all about

フラグメントシェーダの出力は、いつでもカラー値です。 出力カラー値は `oc` レジスタに格納します。

===== AGAL コード詳細

フラグメントシェーダーの実際のコードを見てみましょう。
*最初の行* はカラー値をテクスチャから読み取っています。

    tex ft0, v0, fs0 <2d, linear>

`fs0` が表すテクスチャーから、`v0` が表すテクスチャ座標でカラー値を読み取ります。その際 `2d, linear` という言うオプション情報が付与されています。
`v0` にテクスチャ座標が保持されている理由は、標準バーテックスシェーダー (`STD_VERTEX_SHADER`) 側でそのように設定しているからです。ここに関しては、まあそう言うものだ、と思ってください。
結果は一時レジスタの `ft0`に保持されます。（ AGAL では、演算結果がいつでも命令の第一引数に格納される事を思い出してください。）

[NOTE]
====
ここでちょっと考えて見ましょう。テクスチャの用意は事前に行なっていません。これはどういう事でしょう？

先に説明した通り、フラグメントフィルターはピクセル毎に実行されますが、フィルターへの入力はオブジェクトが一度内部的にテクスチャとして描画されたものなのです。
//原文：As I wrote above, a fragment filter works at the pixel level; its input is the original object, rendered into a texture.
ベースクラスの _FilterEffect_ がその工程を行なっています。プログラムが実行される時、テクスチャーサンプラー `fs0` はフィルター処理の対象となるテクスチャを差し指しています。
//原文：Our base class (_FilterEffect_) sets that up for us; when the program runs, you can be sure that the texture sampler `fs0` will point to the pixels of the object being filtered.
====

さて、この行については多少変更したい点があります。
//原文：You know what, actually I'd like to change this line a little.
行の最後にオプション指定があるのは気づいているかと思います。これはどのようにテクスチャデータを解釈するか指定しているものです。
しかし、対象となるテクスチャのタイプによって、このオプション内容は変更しなくてはいけない事が判明しました。
どんなテクスチャを扱っていても同じコードで動くようにするため、ヘルパーメソッドを用意しましたので、これを使いましょう。
//原文：To be sure the code works for every texture, let's use a helper method to write that AGAL operation.

[source, as3]
----
tex("ft0", "v0", 0, this.texture)
----

この 'tex' メソッドは、先のコードと同じ振る舞いをする AGAL 文字列を返します。これでオプションの内容についてはもうケアする必要がなくなりました。
今後テクスチャにアクセスする際はいつでもこのメソッドを利用してください。（夜にぐっすり眠れるようになりますよ。）

そして２行目は、今回のフィルターのキモの部分です。カラーオフセット値をテクセルカラーに加算します。
オフセット値は `fc0` に保持されています。テクセルカラー値を保持する `ft0` レジスタの値と加算されて、出力レジスタの `oc` に代入されています。

    add oc, ft0, fc0

AGAL コードはまずはこのような感じとなりました。
今度は as コードの、オーバーライドされたメソッドを見てみましょう。

==== beforeDraw

`beforeDraw` メソッドはシェーダーが実行される前に呼び出されます。シェーダーに必要なデータを前もって設定しておく事ができます。

[source, as3]
----
override protected function beforeDraw(context:Context3D):void
{
    context.setProgramConstantsFromVector(Context3DProgramType.FRAGMENT, 0, _offsets);
    super.beforeDraw(context);
}
----

この部分では、フラグメントシェーダーにカラーオフセット値を定数として渡しています。
２番目のパラメータの `0` は、データが格納されるレジスタを意味しています。
シェーダーのコードを見返すと、オフセット値を `fc0` から読み取っています。それがデータを格納した `fragment constant 0` です。

super クラスの beforeDraw 呼び出しでは残りのセットアップを行います。テクスチャを `fs0` に設定したり、テクスチャ座標を設定したりします。

NOTE: 尋ねられる前に、、`afterDraw()` メソッドも同様に存在します。通常このメソッドは利用したリソースを開放するのに使われます。
しかし、定数に対して解放処理は必要ありません。よって今回はこの部分の実装を省きます。

=== 動作確認

さあ、フィルターの実装ができました！（完全なコードは https://gist.github.com/PrimaryFeather/ba1e26d568320cd31086[ここ] から手に入ります。）
早速テストしてみましょう。

[source, as3]
----
var image:Image = new Image(texture);
var filter:ColorOffsetFilter = new ColorOffsetFilter();
filter.redOffset = 0.5;
image.filter = filter;
addChild(image);
----

.フィルターの見た目はちょっとがおかしな状態のようです。
image::customfilter-pma.png[Custom Filter PMA Issue]

なんて事でしょう！
たしかに設定どおり赤味は強まっています。しかしなぜ、鳥の外側のエリアまでそれが広がってしまっているのでしょう？
アルファ値は全くいじっていないというのに。

…慌てないでください。
まだ最初のフィルターを作ったばかりです。それにそんなに大げさな事ではありませんよ。
これは意味のある事なのです。予想されていた事で、これから微調整して修正する部分です。

描画がおかしくなった理由ですが、"premultiplied alpha" (PMA) という仕組みについて考慮するのを忘れていました。
全てのテクスチャは通常、RGB値がアルファ値であらかじめ乗算された状態で保持されているのです。
50％アルファ値の赤は下記のようになりますが、

  R = 1, G = 0, B = 0, A = 0.5

実際は下記のように保存されています。

  R = 0.5, G = 0, B = 0, A = 0.5

この状態を考慮に入れていませんでした。
対応すべき事は、オフセットRGB値を足す前に現在の対象ピクセルのアルファ値を乗算してやる事です。
下記のように実装する事ができます。

[source, as3]
----
tex("ft0", "v0", 0, texture)   // カラー値をテクスチャから読み取る
mov ft1, fc0                   // ft1 にカラーオフセット値をコピーする
mul ft1.xyz, fc0.xyz, ft0.www  // オフセット値のRGB要素にテクスチャのアルファ値を乗算する (pma!)
add  oc, ft0, ft1              // オフセット値をテクスチャに加えてアウトプットとする
----

このように、レジスタの `xyzw` プロパティに個別アクセスする事が可能です。それぞれ `rgba` チャンネルの内容を表します。

NOTE: もしもテクスチャが PMA 形式で保持されていなかったらどうすればいいでしょう？
`tex` メソッドは 必ず PMA 形式のテクスチャを返すので、その事を心配する事はありません。

==== 改めて動作確認

今度はアルファが正しく描画される結果を得る事ができました。（フィルターの完全なコードはここにあります： https://gist.github.com/PrimaryFeather/31f1dd7f04cd6ce886f1[ColorOffsetFilter.as] ）

.良い感じなりました！
image::customfilter-pma-solved.png[Custom Filter with solved PMA issue]

おめでとうございます。完璧に動くフィルターを作り終える事ができましたね。
（実は Starling の `ColorMatrixFilter` を使う事でも同じ結果を得る事はできるのですが、今回作ったフィルターのほうが若干高速に動作します。よって努力の甲斐はあるのです。）

さて、フィルターが作れた事に満足しましたか？次はメッシュスタイルを作る事に挑戦してはどうでしょう。
大きく変わる点はありません。約束します！
