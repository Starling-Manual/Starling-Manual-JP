== Distance Field レンダリング

何度か述べましたが、ビットマップフォントは Starling の中で最も速く描画されます。
しかし、様々な大きさでテキストを表示しなくてはならない時、すぐにビットマップフォントが美しく拡大・縮小されないことに気づくでしょう。
拡大はフォントのぼやけを生み出し、縮小は輪郭のジャギーを生じさせます。
よって、最も美しい結果を得るためにも、全てのサイズのフォントを作成するコンテンツに埋め込んでしまうのが良い方法となります。

_Distance Field レンダリング_  はこの問題を解決します。
この機能は、フォント・または単色のシェイプを、ジャキーの描画や激しい拡大時の描画劣化なしに描画することができます。
このテクニックはValve Softwareによる http://tinyurl.com/AlphaTestedMagnification[SIGGRAPH の発表] で初めて紹介されました。
Starling は _MeshStyle_ を通じてこの機能を提供します。

この機能がどのような仕組みで動いているのか知るために、まず１枚の画像を使った例で説明します。
例えば、コンテンツ内を通して利用するアイコンなどに適用する事ができます。

=== １枚絵をレンダリングする

このマニュアルにはここまでたくさんのトリ達に登場してもらっているので、ここでは鳥の捕食者に登場してもらいます。
うちのメス猫がちょうどよいでしょう。今回の例としてぴったりの黒いベクターデータとして彼女の肖像画を手に入れました。

.うちの猫は "Seven of Nine" と言います。よろしくね。
image::cat.png[Cat, 200]

残念ながらStarlingはベクターデータを表示することができません。
セブン（猫の名前です）をPNGフォーマットのビットマップデータとして必要とします。
ビットマップをそのオリジナルサイズかその近辺で表示した場合、とてもうまく（綺麗に）表示されます。
しかしながら、画像を拡大するとすぐにボケた状態になってしまいます。

.現存のテクスチャの仕組みでは拡大時にボケが生じてしまいます。
image::cat-scale.png[Scaled Cat, 400]

// Image credits: https://thenounproject.com/search/?q=cat&i=657985

このボケは、画像をdistance field テクスチャに変換することによって回避することができます。
実はStarlingフレームワークはこの変換作業を行う小さな便利ツールを同梱しています。
"Field Agent" と呼ばれるもので、Starlingリポジトリの`util`ディレクリ以下で見つけることができます。

NOTE: Field Agentを使うためには _Ruby_ と _ImageMagick_ のインストールが必要です。
付属の `README` ファイルを読み、インストール方法を確認してください。ツールは Windows でも macOS も動きます。

私は高解像度の猫画像をPNGで用意して、Field Agent に適用しました。

  ruby field_agent.rb cat.png cat-df.png --scale 0.25 --auto-size

このコマンドを実行すると元画像の25%の大きさでdistance field形式のテクスチャーが作られます。
field agent は高い解像度の画像を小さく変換する際に一番うまく動作します。
distance field は形状の詳細をエンコードするので、もとのテクスチャよりもかなり小さい状態とすることができます。

.distance field 形式テクスチャへの変換後。
image::cat-distance-field.png[Cat Distance Field Texture]

元画像のシャープな猫のアウトライン形状は、グレデーションのかかったボケにすげ変わっています。
これが distance field というものです。各ピクセルごとに元の形状への一番近い距離の値をエンコードしています。

NOTE: 実際のテクスチャは透明の背景の上に白い色の形状が乗っているものです。上の図ではわかりやすくするために背景を黒く塗りつぶしました。

ボケの幅は _スプレッド_ と呼ばれます。field agent はデフォルトで8ピクセルの幅を用いますが、カスタマイズが可能です。
スプレッドが多いほど良い拡大結果が得られ、特殊効果(後述)を追加しやすくなりますが、
どれだけの幅を持たすことができるかは変換元の画像の形状によってきます。
もし元画像がとても細いライン部分を含んでいた場合、 大きく拡大するのに十分な情報を含ませる領域が足りない事となります。

このテクスチャを Starling で表示するには、単純にテクスチャをロードしてイメージに適用した後、DistanceFieldStyle をそのイメージに適用します。
Starling はこれによりレンダリング方法を distance field 形式に切り替えます。

[source, as3]
----
var texture:Texture = assets.getTexture("cat-df");
var image:Image = new Image(texture);
image.style = new DistanceFieldStyle();
image.color = 0x0; // we want a black cat
addChild(image);
----

このスタイルが設定されると、テクスチャはかなり拡大したとしても完璧にくっきりと表示されます。
ネコの頭の毛の部分のようにディティールの細かい部分においてのみ小さな画像の荒れが確認できるかと思います。

.distance field テクスチャによる拡大表示。
image::cat-scale-df.png[Scaled cat using a distance field texture, 400]

テクスチャ変換コマンドを実行した際の "spread" 値によっては、
気にいったシャープさ・スムーズさに見た目に調整するために `softness` パラメータを変更する必要があるかもしれません。
`softness` パラメータは style クラスをインスタンス化する際の第一引数です。

TIP: 目安となる計算式: `softness = 1.0 / spread`

==== レンダーモード

これが実際の distance field テクスチャーの基本的な使い方となります。distance field スタイルはいくつかのレンダーモードをサポートします。
レンダーモードとは、アウトライン、ドロップシャドウ、グロウのエフェクト付き描画方法の事です。
これらのエフェクトは特別なフラグメントシェーダーで描画されるので、追加のドローコールを必要としません。
つまり、これらのエフェクトは基本的にパフォーマンスを気にせず使う事ができます！

[source, as3]
----
var style:DistanceFieldStyle = new DistanceFieldStyle();
style.setupDropShadow(); // or
style.setupOutline(); // or
style.setupGlow();
----

.distance field スタイルの異なるレンダーモードで描画されたネコ。
image::cat-modes.png[Cat rendered with different modes, 500]

ね、すごいでしょう？

NOTE: 一つだけ制限があります。これらのモードは組み合わせる事ができません。
例えば、アウトラインとドロップシャドウを同時に適用する事はできないわけです。
しかしながら、合わせてフラグメントフィルターを用いる事で（※訳者注：アウトラインを distance field で描画し、
ドロップシャドウをフィルタで描画するなど）実現する事ができます。

=== Distance Field Fonts

The characteristics of distance field rendering makes it perfect for text.
Good news: Starling's standard bitmap font class works really well with the distance field style.
It's just a little cumbersome to create the actual font texture, I'm afraid.

Remember, a bitmap font consists of an atlas-texture that contains all the glyphs and an XML file describing the attributes of each glyph.
You can't simply use _field agent_ to convert the texture in a post-processing step (at least not easily), since each glyph requires some padding around it to make up for the _spread_.

Therefore, it's best to use a bitmap font tool that supports distance field textures natively.
Here are some possible candidates:

* http://kvazars.com/littera/[Littera] -- a free online bitmap font generator.
* http://github.com/libgdx/libgdx/wiki/Hiero[Hiero] -- a cross platform tool.
* http://www.angelcode.com/products/bmfont/[BMFont] -- Windows-only, from AngelCode.

Personally, I achieved the best results with _Hiero_, although its user interface isn't exactly a joy to work with.
I hope that the offerings will improve in the future.

TIP: As for _Hiero_, https://github.com/libgdx/libgdx/wiki/Distance-field-fonts[here] is a very good introduction describing the process.
Unfortunately, _Hiero_ can't export the XML format that Starling requires; this little https://gist.github.com/tluyben/4984856[perl script] might help, though.

Whatever tool or process you use: at the end, you will have a texture and a `.fnt`-file, just as usual.
As a reminder, here's the code to create and register a bitmap font:

[source, as3]
----
[Embed(source="font.fnt", mimeType="application/octet-stream")]
public static const FontXml:Class;

[Embed(source="font.png")]
public static const FontTexture:Class;

var texture:Texture = Texture.fromEmbeddedAsset(FontTexture);
var xml:XML = XML(new FontXml());
var font:BitmapFont = new BitmapFont(texture, xml)
TextField.registerCompositor(font);

var textField:TextField = new TextField(200, 50, "I love Starling");
textField.format.setTo(font.name, BitmapFont.NATIVE_SIZE);
addChild(textField);
----

Up until this point, there's nothing new.
To switch to distance field rendering, we attach the appropriate style right to the TextField.

[source, as3]
----
var style:DistanceFieldStyle = new DistanceFieldStyle();
textField.style = style;
----

The reward for all this hard work: such a font can now be used at almost any scale, and with all the flexible render modes I showed above.

.A bitmap font using distance fields looks great at any scale.
image::distance-field-scale.png[Scaled TextField with a Bitmap Font]

